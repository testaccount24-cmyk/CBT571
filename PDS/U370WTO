U370WTO  TITLE 'Issue WTO'
*
* Copyright 2002 James M. Morrison
*
*----------------------------------------------------------------------
         U#SET FUNC=UTIL
*----------------------------------------------------------------------
* Function:    Issue Write To Operator (WTO, SVC 35)
*              Echoes WTO text in xxxxLOG via ULOG
*
*              Supports Helper macro: UWTO
*
* Entry:  R0   bits
*
*              00:23   currently ignored
*
*              24:31   option bits as described in WATGD:
*
*                      UWTO$DSC - descriptor codes valid
*                      UWTO$RTC - routing    codes valid
*
*         R1 = address of following:
*
*         -08  (02)    WPLLGH
*         -06  (02)    MCS flags
*         -05  (01)    ignored - msg 'x'
*         -04  (02)    descriptor code or x'0000' (default  8)
*         -02  (02     routing    code or x'0000' (default 11)
*         +00  (01)    text length (truncated after 124 bytes)
*         +01  (nn)    message text
*
* Exit:   R15 = 0
*
* Notes:  Does not support multi-line WTOs or WTOR
*         Mostly used when WTO text is variable
*
*         The parmlist to this routine is intended to make things
*         as easy as possible for the caller.  If all that is
*         desired is to issue a WTO with given text and take the
*         defaults for everything else, only the +00 and +01
*         elements are needed, with R1 pointing at the one byte
*         length & R0 = x'00'.
*
*         If the caller wishes to specify any of the elements in
*         the prefix portion (negative offsets), a WTO MF=L
*         macro can be expanded in the caller's CSECT to set
*         up our parmlist, with the text length and desired text
*         immediately behind the expansion, R1 pointing at the
*         one byte length code at +00 & R0 set to select the
*         desired prefix elements.
*
*         Currently, the caller's MCS flags are ignored.
*
*----------------------------------------------------------------------
* Reg  DSECT    usage
*----------------------------------------------------------------------
* R2            spare
* R3            @ text length, text
* R4            @ caller's desc & routing codes, if any
* R5            00:15 - desc, 16:31 - rout
* R6            text length, WPLLGH
* R7            effective text length
* R8            @ WTO parmlist desc & rout codes
* R9            l' desc & routing codes (4)
* R10           zap reg
*----------------------------------------------------------------------
U370WTO  UPROC ID=UWTO,L=L$SA
*----------------------------------------------------------------------
         STM   R0,R2,SAENTRY           save entry parms
         LR    R3,R1                   @ length, text
         LR    R4,R1
         UDIAG 1,'U370WTO enter'
         LA    R9,L'WPLDESC+L'WPLROUT  4
         SR    R4,R9                   @ desc & routing codes
*
         ICM   R5,12,DFLTDESC          load default desc code
         ICM   R5,03,DFLTROUT          load default rout code
         TM    SAR0B3,UWTO$DSC         descriptor codes present?
         BNO   *+8
         ICM   R5,12,0(R4)             load caller's desc codes
*
         TM    SAR0B3,UWTO$RTC         routing codes present?
         BNO   *+8
         ICM   R5,03,2(R4)             load caller's rout codes
*
         SLR   R6,R6
         ICM   R6,1,0(R3)              text length
         BZ    EXIT
         LA    R15,L'SATEXT
         CR    R6,R15                  caller's text too long?
         BL    *+6
         LR    R6,R15                  truncate at max
*
         LR    R7,R6                   effective text length
         MVC   SAMCS,MCSFLGS           set MCS flags
         BCTR  R6,0
         EX    R6,COPYTEXT             copy WPLTXT
         LA    R6,1(R9,R6)
         STH   R6,SAWTOLGH             set WPLLGH
*
         LA    R8,SATEXT(R7)           @ desc & rout code slot
         STCM  R5,15,0(R8)             save desc & rout codes
*
         WTO   MF=(E,SAWTO)            issue WTO
*
         MVC   SATEXT-06(06),=C'<WTO> '
         LA    R7,6(,R7)
         UDIAG 0,SATEXT-06,(R7)        echo WTO to xxxxLOG
*
EXIT     DS    0H                      return to caller
         UDIAG 1,'U370WTO exit'
         UPEND ,                       return to caller
*----------------------------------------------------------------------
*  Data Areas
*----------------------------------------------------------------------
COPYTEXT MVC   SATEXT(*-*),1(R3)       EX'd: copy caller's WTO text
MDLWTO   WTO   'x',ROUTCDE=11,DESC=8,MF=L
LWTOTXT  EQU   MDLWTO+0,2              text length + 4
MCSFLGS  EQU   MDLWTO+2,2              MCS flags
WTOTXT   EQU   MDLWTO+4,1              text
DFLTDESC EQU   MDLWTO+5,2              default descriptor code
DFLTROUT EQU   MDLWTO+7,2              default routing    code
         LTORG ,
*----------------------------------------------------------------------
*  Savearea & workarea
*----------------------------------------------------------------------
SAVEAREA DSECT ,
SASA     DS    18F
SAENTRY  DS    3F                      R0:R1 at entry
SAR0     EQU   SAENTRY+(R0*4),4
SAR0B0   EQU   SAR0+0,1
SAR0B1   EQU   SAR0+1,1
SAR0B2   EQU   SAR0+2,1
SAR0B3   EQU   SAR0+3,1
*
SAWTO    WTO   'x',ROUTCDE=11,DESC=8,MF=L
         ORG   SAWTO
SAWTOLGH DS    H                       combined length: text, length,
*                                      and MCS flags (L' text + 4)
SAMCS    DS    H                       MCS flags
SATEXT   DS    CL124                   text follows (max 124 or 126,
*                                      depending on whether you believe
*                                      GC28-0683-2 OS/VS2 Supervisor
*                                      Services and Macro Instructions
*                                      or the IEZWPL macro)
*                                      we pick the more restrictive 124
*                                      from Supvr Services & Macros
*
SADESC   DS    XL2                     descriptor codes immediately
*                                      follow last byte of msg text
SAROUT   DS    XL2                     routing codes behind desc codes
         ORG   ,
         DS    0D                      align
L$SA     EQU   *-SAVEAREA              length of savearea
*
         PRINT NOGEN
         WATGD ,
         PRINT GEN
         IEZWPL ,                      WTO parmlist - SYS1.AMODGEN
U370WTO  UFINI DXDUWTO,L$SA
         END   ,
