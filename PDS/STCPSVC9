STCPSVC9 TITLE 'TCP/IP SVC termination'
*
* Copyright 2002 James M. Morrison
*
*----------------------------------------------------------------------
*  To do:
*----------------------------------------------------------------------
         U#SET FUNC=STCP
*----------------------------------------------------------------------
* Function:    Restores SVCTABLE for our stolen SVC entry
*              with the original contents of SVCTABLE entry
*
* Entry:  no parms, supervisor state, key 8
*
* Exit:   R15 =  0 - OK; supervisor state, key 8
*
* Notes:  See STCPSVC0 for notes & SVCTABLE steal
*
*----------------------------------------------------------------------
STCPSVC9 UPROC ID=SS00,L=L$SA
         UDIAG 1,'SVC9 enter'
         L     R3,STP@SVCT             @ stolen SVC entry
         LTR   R3,R3
         BNZ   DEINST
         UDIAG 1,'SVC9 TCP/IP SVC not installed'
         B     GETSGD
*
DEINST   SPKA  0
         MVC   0(L$SVC,R3),STPSVCTB    restore SVCTABLE entry
         XC    STPSVCX,STPSVCX         clear stolen entry
         UDIAG 2,'SVC9 TCP/IP SVC restored'
*
GETSGD   L     R4,STP@SGD              @ ss SGD
         USING SGD,R4
         LTR   R4,R4
         BNZ   REPGONE
         UDIAG 1,'SVC9 SGD not available'
         B     EXIT
REPGONE  MVC   SGDSVC(4),GONE          replace SVC with GONE instr
*
*  If STCPSS00 allocated CSA storage for the SVC, free it
*
         L     R0,SGDLSVC
         LTR   R0,R0
         BZ    EXIT
         L     R1,SGD@SVC
         LTR   R1,R1
         BZ    EXIT
         FREEMAIN R,LV=(0),A=(1)       release SVC GETMAIN space
         UDIAG 2,'SVC9 freed CSA'
*
EXIT     SPKA 8
         UDIAG 1,'SVC9 exit'
         UPEND ,                       return to caller
*----------------------------------------------------------------------
GONE     LA    R15,20                  replaces STPSVC when SVC removed
         EJECT ,
*----------------------------------------------------------------------
*  Data areas
*----------------------------------------------------------------------
         LTORG ,
SAVEAREA DSECT ,
SASA     DS    18F
SAREASON DS    X
         DS    0D
L$SA     EQU   *-SAVEAREA
*
         WASTCP ,                      TCP/IP common
         DSSSGD SECT=DSECT
         PRINT NOGEN
         IHASVC ,
L$SVC    EQU   *-SVCENTRY              length of SVCTABLE entry
STCPSVC9 UFINI DXDSSVC9,L$SA
         END   ,
