XMITBUF  TITLE 'SPLIT buffer into segment(s), write'
*
* Copyright 2001, 2002 James M. Morrison
*
*----------------------------------------------------------------------
*  Things to do:
*----------------------------------------------------------------------
         U#SET FUNC=XMIT
*----------------------------------------------------------------------
* Function: Given a BSAM buffer, split the buffer into segment(s),
*           and write the segments by calling XMITPUT (PUTSEG).
*
* Entry:  AMODE=24, ASC=PRIMARY, KEY=*, STATE=*
*
*         R0  = length of buffer
*         R1  = address of buffer
*
* Exit:   R15 = return code
*         R15 = 00 success
*
* Abends:
*
* Notes:
*----------------------------------------------------------------------
* R2     XBUWA
* R3     2 (constant)
* R4     @ segment data
* R5     length remaining in segment data
* R6     length of input buffer = blksize
* R7     address of input buffer
* R8     length of segment to be written
* R9     253 (constant)
* R10
* R11    XMTWA
* R12    base reg
* R13    savearea
*----------------------------------------------------------------------
         TITLE ' '
*----------------------------------------------------------------------
XMITBUF  UPROC L=L$XBUWA,ID=XBUF,WA=XBUWA,INIT=MVCL
         LR    R6,R0
         LR    R7,R1
         UDIAG 1,'XMITBUF entered'
         UDIAG 3,'XMITBUF input block',(R7),(R6)
*----------------------------------------------------------------------
* Create segment
*----------------------------------------------------------------------
         LA    R3,L'XBUSEGL+L'XBUSEGF  l' overhead
         LA    R9,L'XBUSEGD            max # seg data bytes permitted
         MVI   XBUSEGF,SEG$1           turn on 'first' flag
SPLIT    DS    0H
         LTR   R6,R6                   anything left in buffer?
         BNP   EXITRC0
         LR    R8,R9                   assume max # bytes in segment
         CR    R6,R9
         BH    NOTLAST
         OI    XBUSEGF,SEG$L           last segment
         LR    R8,R6
NOTLAST  DS    0H
         AR    R8,R3                   segment length (except flag)
         STC   R8,XBUSEGL
         SR    R8,R3
         BCTR  R8,0
         EX    R8,COPY                 copy buffer data to segment
         LA    R8,1(,R8)
         LA    R7,0(R8,R7)             bump to next avail char
         SR    R6,R8
*----------------------------------------------------------------------
* Write INMR03 segment
*----------------------------------------------------------------------
WRITE    DS    0H
         LA    R1,XBUSEG
         UCALL XMITPUT                 write INMR03 segment
         NI    XBUSEGF,255-SEG$1       not first segment anymore
         LTR   R15,R15
         BZ    SPLIT
         BNZ   EXIT
*
EXITRC0  DS    0H
         SLR   R15,R15
*
EXIT     DS    0H
         ST    R15,XBURC
         UDIAG 1,'XMITBUF exited'
         L     R15,XBURC
         UPEND ,
*
COPY     MVC   XBUSEGD(*-*),0(R7)      copy data
*
         LTORG ,
         EJECT ,
*----------------------------------------------------------------------
XBUWA    DSECT ,
XBUSA    DS    18F
*
XBURC    DS    F             return code
XBUSEG   DS    0D            segment
XBUSEGL  DS    X             segment length
XBUSEGF  DS    X             segment flag
XBUSEGD  DS    XL253         segment data
         DS    0D
L$XBUWA  EQU   *-XBUWA
         EJECT ,
         PRINT NOGEN
         DXSEG ,
         EJECT ,
         WAXMIT ,
         PRINT GEN
XMITBUF  UFINI DXDXBUF,L$XBUWA
         END   ,
