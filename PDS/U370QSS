U370QSS  TITLE 'Query Subsystem'
*
* Copyright 2002 James M. Morrison
*
*----------------------------------------------------------------------
*  To do:
*  1) Call DISPSS for each subsys (loop thru subsys again)
*----------------------------------------------------------------------
         U#SET FUNC=UTIL
*----------------------------------------------------------------------
* Function:    Diagnostic aid - display subsys info
*              Currently controlled by debug level
*
* Entry: Enabled, Supervisor state, Key 8
*        R0    ignored
*        R1    ignored
*
* Exit:  Enabled, Supervisor state, Key 8
*        R15 = return code = 00
*
* Diagnostics:
*              1 - ABEND retry entered message
*              2 - subsys info
*
* Notes:
*----------------------------------------------------------------------
* Reg  DSECT    usage
*----------------------------------------------------------------------
* R2   CVT
* R3   JESCT
* R4   SSCT     SSCVT
* R5   SSVT
* R6
* R7
* R8
* R9            subroutine scratch
* R10           internal linkage
*----------------------------------------------------------------------
         TITLE ' '
U370QSS  UPROC ID=UQQQ,L=L$SA
         UDIAG 1,'QSS entered'
*----------------------------------------------------------------------
         STM   R0,R2,SAENTRY           save entry parms
         UDIAG 2,SKIP=EXIT             leave now if no real output
         BAL   R10,CLEAR               clear line to blanks
         BAL   R10,PRINTX              print blank line
         UDIAG 2,'QSS:'               output follows
*----------------------------------------------------------------------
*  Print CVT, JESCT, SS-name, SSCVT, SSVT
*----------------------------------------------------------------------
         USING PSA,0
         MVC   SAM1TXT2(3),=C'CVT'
         MVC   SAM1TXT3(5),=C'JESCT'
         MVC   SAM1TXT4(6),=C'SUBSYS'
         MVC   SAM1TXT5(5),=C'SSCVT'
         MVC   SAM1TXT6(8),=C'SSCTSSVT'
         MVC   SAM1TXT7(8),=C'SSCTSUSE'
         MVC   SAM1TXT8(8),=C'SSCTFLG1'
         BAL   R10,PRINT               print title line
*
QCVT     L     R2,FLCCVT               @ CVT
         USING CVT,R2
         LR    R0,R2
         LA    R1,SAM1TXT2
         BAL   R10,FMTHEX
*
QJESCT   L     R3,CVTJESCT             @ JESCT
         USING JESCT,R3
         LR    R0,R3
         LA    R1,SAM1TXT3
         BAL   R10,FMTHEX
*
         L     R4,JESSSCT              @ 1st SSCVT
         USING SSCT,R4
CHASLOOP DS    0H
         MVC   SAM1TXT4(4),SSCTSNAM    subsys name
         LR    R0,R4
         LA    R1,SAM1TXT5
         BAL   R10,FMTHEX
*
         L     R5,SSCTSSVT             @ subsystem's SSVT
         USING SSVT,R5
         LR    R0,R5
         LA    R1,SAM1TXT6
         BAL   R10,FMTHEX
*
         L     R0,SSCTSUSE             @ subsystem's user field
         LA    R1,SAM1TXT7
         BAL   R10,FMTHEX
*
         L     R0,SSCTFLG1             flag byte + reserved
         LA    R1,SAM1TXT8
         BAL   R10,FMTHEX
*
         BAL   R10,PRINT               print data line
*
         ICM   R4,15,SSCTSCTA          @ next subsystem's SSCVT
         BNZ   CHASLOOP
         EJECT ,
*
EXIT     DS    0H
         UDIAG 1,'QSS exit'
         UPEND ,                       return to caller
         EJECT ,
*----------------------------------------------------------------------
*  Format fullword (display hex) - R0 = value, R1 @ output slot
*----------------------------------------------------------------------
FMTHEX   DS    0H
         UCALL U370HEX
         BR    R10
*----------------------------------------------------------------------
*  DISPSS  - Display SSCVT (R4), SSVT (R5); R9 scratch
*----------------------------------------------------------------------
DISPSS   DS    0H
         UDIAG 3,'QSS SSCVT',(R4),24
         LTR   R5,R5
         BZR   R10
         LH    R9,SSVTFNUM             # function routines
         SLL   R9,2                    times size of A-con
         LA    R9,256+4(,R9)           plus matrix & overhead
         UDIAG 4,'QSS SSVT',(R5),(R9)
         UDIAG 3,' '
         BR    R10
*----------------------------------------------------------------------
*  Print message
*----------------------------------------------------------------------
PRINT    DS    0H
         MVC   SAM1TXT1,=C'<2>'
PRINTX   UDIAG 2,SAMSG,L$SAMSG
CLEAR    MVI   SAMSG,C' '
         MVC   SAMSG+1(L$SAMSG-1),SAMSG
         BR    R10
         LTORG ,
*----------------------------------------------------------------------
*  Data areas
*----------------------------------------------------------------------
         LTORG ,
*----------------------------------------------------------------------
*  Dynamic savearea & workarea - based R13
*----------------------------------------------------------------------
SAVEAREA DSECT ,
SASA     DS    18F                standard MVS savearea
SAENTRY  DS    3F                 R0:R2 from entry
*
SAM1TXT1 DS    C'<3> '
SAM1TXT2 DS    CL9                CVT
         DS    CL2
SAM1FRST DS    0C
SAM1TXT3 DS    CL8                JESCT
         DS    CL2
SAM1TXT4 DS    CL8                SS name
         DS    CL2
SAM1TXT5 DS    CL8                @ SSCVT
         DS    CL2
SAM1TXT6 DS    CL8                SSCTSSVT
         DS    CL2
SAM1TXT7 DS    CL8                SSCTSUSE
         DS    CL2
SAM1TXT8 DS    CL8                SSCTFLG1
         DS    CL2
SAM1TXTP DS    CL2
         DS    CL2
SAM1TXTF DS    CL11
SAM1LAST DS    0C
L$SAMSG  EQU   *-SAM1TXT1
SAMSG    EQU   SAM1TXT1,L$SAMSG
SAX      DS    0D
L$SA     EQU   *-SAVEAREA         length of savearea
*
         WATGD ,
*
         PRINT NOGEN
         IHAPSA ,
         CVT   DSECT=YES,LIST=NO
         IEFJESCT ,
         IEFJSCVT ,
         IEFJSSVT ,
*
U370QSS  UFINI DXDUQSS,L$SA
         END   ,
