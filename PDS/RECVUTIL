RECVUTIL TITLE 'Call UTILITY'
*
* Copyright 2002 James M. Morrison
*
*----------------------------------------------------------------------
*  To do:
*----------------------------------------------------------------------
         U#SET FUNC=RECV
*----------------------------------------------------------------------
* Function: Given a CS2 (RECVCS2), call the indicated UTILITY
*           If multiple utilities apply to the same file #, call
*           them all
*
* Entry:  S/370 ECMODE KEY=8, STATE=PROB, AUTHORIZED
*
*         R0  =
*         R1  = address of CS2
*
* Exit:   R15 = return code
*
*               00   success
*                    R1 = 0 - no control segment emitted
*
*               04   control segment returned (everything else OK)
*                    R1 = address of control segment
*
*               08   Utility return code > 4
*                    R1 = 0 or @ control segment
*
*               12   Utility not recognized
*                    R1 = 0 or @ control segment
*
*               16   Utility LOAD failed
*                    R1 = 0 or @ control segment
*
* Notes: Sets CS2DONE to indicate CS2 has been handled
*----------------------------------------------------------------------
* R2     return code
* R3     scratch
* R4
* R5     BXLE
* R6     BXLE
* R7     BXLE
* R8
* R9
* R10    CS2
*----------------------------------------------------------------------
RC#0     EQU   0        everything ok
RC#4     EQU   4        control segment emitted; R1 @ segment
RC#8     EQU   8        utility rc > 4
RC#12    EQU   12       unsupported utility
RC#16    EQU   16       LOAD of utility failed
*
ERR#1    EQU   1        support routine unresolved
ERR#2    EQU   2        returned ctl segment SEGL=0
ERR#3    EQU   3        bad XMIT; two INMCOPYs for same file
ERR#4    EQU   4        LOAD error
         TITLE ' '
*----------------------------------------------------------------------
RECVUTIL UPROC L=L$SA,ID=RUTL,WA=SAVEAREA,INIT=(XC,2)
*----------------------------------------------------------------------
*  Initialize
*----------------------------------------------------------------------
INIT     DS    0H
         STM   R0,R1,SAENTRY
         SLR   R2,R2                        assume good return code
         LR    R10,R1                       save CS2
         USING CS2,R10
         UDIAG 1,'UTIL enter'
         UDIAG 2,'UTIL CS2 at entry',(R10),L$CS2
SCANREGS LM    R5,R7,=A(TAB,12,TABEND-1)    BXLE regs
*
SCAN     DS    0H
         CLC   CS2UTILN,0(R5)               supported UTILITY?
         BE    FOUND
         BXLE  R5,R6,SCAN
         UDIAG 0,'RECVUTIL unsupported utility (CS2)',CS2,L$CS2
         LA    R2,RC#12
         B     EXITRC
*
FOUND    DS    0H
         MVI   CS2DONE,CS2$DONE             show CS2 handled
         ICM   R15,15,8(R5)
         BNZR  R15                          call support routine
         MVI   SAREASON,ERR#1               support routine unresolved
DIE      UABEND SAREASON                    ABEND w/ reason code
*----------------------------------------------------------------------
*  See if another utility deals with same file #, if so, call it
*----------------------------------------------------------------------
UTNEXT   DS    0H                           see if more CS2s qualify
         L     R0,CS2#FILE                  file # we just handled
         LA    R14,L$CS2                    length of CS2
         SR    R10,R14                      back up to previous CS2
         C     R0,CS2#FILE
         BNE   EXITRC                       different file #, done
         UDIAG 2,'UTIL selected CS2',(R10),L$CS2
         B     SCANREGS
         EJECT ,
*----------------------------------------------------------------------
*  INMCOPY - copy segment data to SYSUT1 or display MESSAGE on LOG
*  INMCOPY is only utility that reads XMIT, so other utility support
*  routines don't need to check if SACTL has already been set
*----------------------------------------------------------------------
INMCOPY  DS    0H
         LA    R0,IEBUT1                    target DDNAME
         LR    R1,R10                       CS2
         L     R15,=V(RECVMSG)              assume embedded message
         CLI   CS2TERM,C'Y'                 embedded message?
         BE    *+8
         L     R15,=V(RECVRCPY)             no embedded message
         BALR  R14,R15                      call RECVMSG or RECVRCPY
         ST    R15,CS2UTRC                  save utility return code
         MVI   SAREASON,ERR#3               two INMCOPYs for same file
         SLR   R0,R0
         C     R0,SACTL                     previously emitted ctl seg?
         BNE   DIE
         ST    R1,SACTL                     0 or @ returned ctl seg
         UPSR  2,'RECVUTIL INMCOPY rc',*CS2UTRC
         L     R0,CS2UTRC
         LTR   R0,R0
         BZ    UTNEXT                       see if another utility
*
         MVI   SAREASON,ERR#2               returned ctl seg SEGL=0
         L     R15,SACTL
         SLR   R14,R14
         ICM   R14,1,SEGL-SEG(R15)
         BZ    DIE
         UDIAG 2,'UTIL INMCOPY emit ctl seg',(R15),(R14)
         B     UTNEXT
         EJECT ,
*----------------------------------------------------------------------
*  IEBCOPY - restore unloaded PDS from SYSUT1
*----------------------------------------------------------------------
IEBCOPY  DS    0H                           set up IEBCOPY parmlist
         LA    R0,SAPRM1
         ST    R0,SAPARM                    IEBCOPY parmlist
*
         LA    R0,IEBOPTS     SAPRM4
         ST    R0,SAPRM1                    IEBCOPY options (OS PARM)
*
         LA    R0,IEBDDLST    SAPRM6
         ST    R0,SAPRM2                    IEBCOPY DDNAME list
*
         LA    R0,IEBPAGEL    SAPRM8
         ST    R0,SAPRM3                    IEBCOPY page header
*
         OI    SAPRM2,128                   VL flag (ignore page hdr)
         B     LOAD
*
*  Extraneous setup, for ease of zapping
*
         LA    R0,0           SAPRM8
         ST    R0,SAPRM3                    IEBCOPY page header
         OI    SAPRM3,128                   VL flag
*
         MVC   SAPRM4,IEBOPTS               set OS PARM length
         MVC   SAPRM5,IEBPARM               copy OS PARM
*
         MVC   SAPRM6,IEBDDLST              set DDNAME list length
         MVC   SAPRM7,IEBDDN                copy DDNAME list
*
         MVC   SAPRM8,IEBPAGEL              set page header length
         MVC   SAPRM9,IEBPAGE               copy page header
         B     LOAD                         common IBM utilty code
*
         DS    0H
IEBOPTS  DC    AL2(0)                       length of OS parm string
IEBPARM  DC    C'                   '       IEBCOPY options (zappable)
*        DC    C'SIZE=nnnnK,WORK=nnK'       SIZE = buffer size
*                                           WORK = I/O buffer size
*
         DS    0H
IEBDDLST DC    AL2(IEBDDX-IEBDDN)           L' DDNAME list
IEBDDN   DS    0H
         DC    XL8'00'
         DC    XL8'00'
         DC    XL8'00'
         DC    XL8'00'
IEBSYSIN DC    CL8'SYSIN'                   SYSIN    DD DUMMY
IEBSYSPT DC    CL8'SYSPRINT'                SYSPRINT DD SYSOUT
         DC    XL8'00'
IEBUT1   DC    CL8'SYSUT1'                  SYSUT1   input unloaded ds
IEBUT2   DC    CL8'SYSUT2'                  SYSUT2   output PDS
IEBUT3   DC    CL8'SYSUT3'                  SYSUT3   spill
IEBUT4   DC    CL8'SYSUT4'                  SYSUT4   spill
IEBDDX   DS    0H
*
         DS    0H
IEBPAGEL DC    AL2(L'IEBPAGE)               PAGE HEADER length
IEBPAGE  DC    Z'1'                         PAGE HEADER
         EJECT ,
*----------------------------------------------------------------------
*  LOAD UTILITY
*----------------------------------------------------------------------
LOAD     DS    0H
         MVI   SAREASON,ERR#4                    assume LOAD error
         LOAD  EPLOC=CS2UTILN,ERRET=LOADFAIL     load UTILITY
         LTR   R15,R15
         BZ    CALL
         B     DIE                          LOAD parm error?!?
*
LOADFAIL DS    0H
         ST    R1,SALDABCD                  save averted ABEND code
         ST    R15,SALDRSN                  save averted ABEND reason
         UDIAG 0,'RECVUTIL utility LOAD failed',SALDBAD,L'SALDBAD
         UDIAG 0,'RECVUTIL CS2',(R10),L$CS2
         LA    R2,RC#16                     load failed rc
         B     EXITRC
*----------------------------------------------------------------------
*  Call UTILITY
*----------------------------------------------------------------------
CALL     DS    0H
         LR    R15,R0                       get entry address
         LR    R9,R15                       save EP
         L     R1,SAPARM                    UTILITY parmlist ptr
         BALR  R14,R15                      call UTILITY
         ST    R15,CS2UTRC                  save return code
         UPSR  2,'RECVUTIL UTILITY rc %',(R15)
*----------------------------------------------------------------------
*  DELETE UTILITY
*----------------------------------------------------------------------
DELETE   DS    0H
         DELETE EPLOC=CS2UTILN     delete UTILITY program
         UPSR  2,'RECVUTIL UTILITY DELETE rc %',(R15)
*
         LA    R0,4                standard IBM utility warning rc
         C     R0,CS2UTRC          worse rc?
         BNL   UTNEXT              no, see if more utilities to call
         LA    R2,RC#8             bad utility return code
*----------------------------------------------------------------------
*  Return to caller
*----------------------------------------------------------------------
EXITRC   DS    0H
         L     R3,SACTL                     0 or @ control seg to emit
         SLR   R0,R0
         CR    R0,R3                        control seg emitted?
         BE    EXIT2                        no, br
         CR    R0,R2                        everything OK so far?
         BNE   EXIT2                        no, br
         LA    R2,RC#4                      yes, set 'emit ctl seg' rc
EXIT2    UPSR  1,'RECVUTIL exited rc',(R2)
         LR    R1,R3
         UPEND RC=(R2),R1=PASS
         EJECT ,
*----------------------------------------------------------------------
*  Data areas
*----------------------------------------------------------------------
TAB      DS    0F
         DC    CL8'INMCOPY',A(INMCOPY)
         DC    CL8'IEBCOPY',A(IEBCOPY)
TABEND   DS    0X
         LTORG ,
*        EJECT ,
*----------------------------------------------------------------------
*  Savearea/workarea
*----------------------------------------------------------------------
SAVEAREA DSECT ,
SASA     DS    18F                     savearea for anyone we call
SARC     DS    F                       our return code
SAENTRY  DS    2F                      R0:R1 at entry
SACTL    DS    A                       addr of ctl seg from RECVRCPY
SAREASON DS    X                       UABEND reason code
*
SALDABCD DS    F                       LOAD ABEND code
SALDRSN  DS    F                       LOAD ABEND reason
SALDBAD  EQU   SALDABCD,8
*
*  General parm layout for calling IBM utilities
*
SAPARM   DS    A                       UTILITY parmlist ptr
*
SAPRM1   DS    A                       OS parm
SAPRM2   DS    A                       DDNAME list
SAPRM3   DS    A                       page header
*
SAPRM4   DS    H                       L' OS PARM
SAPRM5   DS    CL100                   UTILITY OS PARM
*
SAPRM6   DS    H                       L' ddname list
SAPRM7   DS    XL100                   UTILITY ddname list
*
SAPRM8   DS    H                       L' page number
SAPRM9   DS    CL4                     page number
*
         DS    0D
L$SA     EQU   *-SAVEAREA
*
         EJECT ,
         DRCS2 ,
*        EJECT ,
         PUSH  PRINT
         PRINT NOGEN
         DXSEG ,
         WARECV ,
*        EJECT ,
         POP   PRINT
RECVUTIL UFINI DXDRUTL,L$SA            pseudo-register
         END   ,
