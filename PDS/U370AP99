U370AP99 TITLE 'xxxx370 Application Termination'
*
* Copyright 2002 James M. Morrison
*
*----------------------------------------------------------------------
*  To do:
*----------------------------------------------------------------------
         U#SET FUNC=UTIL
*----------------------------------------------------------------------
* Function:    Terminate xxxx370 application
*              Called via UPEND ID=MAIN
*
*              - Calls XXXXAP99 (WXTRN), if exists
*                Application-specific cleanup
*
*              - Calls selected U370* CSECTs to cleanup
*
*              - Re-establishes problem state, key 8
*
*              - Releases DSA pool
*
*              - Doesn't return to ID=MAIN CSECT; returns to
*                ID=MAIN CSECT's caller directly
*
* Entry: R0      ignored
*        R1      ignored
*
*        TGDFLG1 Two flags are supported:
*
*                TGD1XNFR  - don't FREEMAIN DSA pool
*                TGD1XABD  - ABEND instead of returning
*
* Exit:  R15 = return code = TGDRC
*----------------------------------------------------------------------
         TITLE ' '
U370AP99 UPROC ID=UAP9,L=L$SA,CPD=(Y,0,ESTAE)
*----------------------------------------------------------------------
*  Call xxxx370 cleanup routine
*----------------------------------------------------------------------
         WXTRN XXXXAP99
         L     R15,=V(XXXXAP99)        appl cleanup
         LTR   R15,R15                 present?
         BZ    SHUT                    no, skip it
*
         BALR  R14,R15                 call appl cleanup (no parms)
*----------------------------------------------------------------------
*  Call U370-guys to cleanup
*----------------------------------------------------------------------
SHUT     DS    0H
         L     R3,TGDRC                application return code
         UPSR  1,'AP99 terminating rc',(R3)
         UDIAG 5,'AP99 DSA pool',(RDSA),*TGDLDSA
         UESTAE  TERM=YES              de-establish ESTAE environment
         ULOG    TERM=YES              close xxxxLOG
         SPKA  8*16                    back to key 8
         MODESET MODE=PROB             back to problem state
         NI    TGDFLG5,255-TGD5SUPR    no longer in supervisor state
*----------------------------------------------------------------------
*  Release DSA pool
*----------------------------------------------------------------------
         LA    R9,DIE                  assume ABEND, not EXIT
         TM    TGDFLG1,TGD1XABD        ABEND instead of return?
         BO    *+8                     yes, br
         LA    R9,EXIT                 no ABEND
*
         L     R13,TGDSA1+4            ID=MAIN caller's R13
         DROP  R13
         TM    TGDFLG1,TGD1XNFR        skip FREEMAIN?
         BO    EXITBR                  yes, br
         L     R0,TGDLDSA              length of DSA pool
         FREEMAIN R,LV=(0),A=(RDSA)    release DSA POOL
         DROP  RDSA
EXITBR   BR    R9                      exit or ABEND
*----------------------------------------------------------------------
*  Return to ID=MAIN's caller
*----------------------------------------------------------------------
EXIT     DS    0H
         L     R14,12(,R13)            restore return address
         LR    R15,R3                  application return code
         LM    R0,R12,20(R13)          restore caller regs
         BR    R14                     return to ID=MAIN's caller
*----------------------------------------------------------------------
DIE      SLR   R15,R15                 reason code = none
         ABEND 99,DUMP                 ABEND w/ dump
*----------------------------------------------------------------------
*  Data areas
*----------------------------------------------------------------------
         LTORG ,
*----------------------------------------------------------------------
*  Dynamic savearea & workarea - based R13
*----------------------------------------------------------------------
SAVEAREA DSECT ,
SASA     DS    18F                savearea
SAX      DS    0D
L$SA     EQU   *-SAVEAREA         length of savearea
         WATGD ,
U370AP99 UFINI DXDUAP99,L$SA
         END   ,
