RECVMSG  TITLE 'RECEIVE INMCOPY - embedded message'
*
* Copyright 2001, 2002 James M. Morrison
*
*----------------------------------------------------------------------
*  Things to do:
*----------------------------------------------------------------------
         U#SET FUNC=RECV
*----------------------------------------------------------------------
* Function: Display embedded MESSAGE on LOG
*
* Entry:  S/370 ECMODE KEY=8, STATE=PROB, AUTHORIZED
*
*         R0  = ignored
*         R1  = address of CS2 representing INMCOPY
*
* Exit:   R15 = return code
*
*               00   success
*               04   control segment returned (everything else OK)
*
*         R1  = zero or address of control segment
*
* Abends: yes
*
* Notes:
*
* Debug:  1  -  entry/exit; show exit rc
*         2  -  CS2 at entry
*         3  -  show returned data block
*----------------------------------------------------------------------
* R2
* R3
* R4
* R5
* R6
* R7
* R8
* R9
* R10    CS2
* R11    RECVWA
*----------------------------------------------------------------------
ERR#1    EQU   1        R1 = 0 at entry
         TITLE ' '
*----------------------------------------------------------------------
RECVMSG  UPROC L=L$SA,ID=RMSG
*----------------------------------------------------------------------
*  Verify parm
*----------------------------------------------------------------------
INIT     DS    0H
         MVI   SAREASON,ERR#1               CS2 parm = 0
         LTR   R10,R1                       save CS2
         BZ    DIE
         USING CS2,R10
         UDIAG 1,'RECVMSG entered'
         UDIAG 2,'RECVMSG INMCOPY CS2',(R10),L$CS2
*----------------------------------------------------------------------
*  Retrieve data block, display it
*----------------------------------------------------------------------
GETBLOCK DS    0H
         LA    R0,L'SABUF              buffer length
         LA    R1,SABUF                buffer address
         UCALL RECVBLK,ERR=READCTL     retrieve data block
         LR    R9,R0                   length of data returned
         ULOG  SABUF,(R9)              display message record
         B     GETBLOCK
*
READCTL  DS    0H
         LR    R2,R15                  save RECVBLK rc
         LR    R3,R1                   save ctl seg addr
*----------------------------------------------------------------------
*  Return to caller
*----------------------------------------------------------------------
EXIT     DS    0H
         UPSR  1,'RECVMSG exited rc',(R2)
         LR    R1,R3
         UPEND RC=(R2),R1=PASS              return R1, R15
*
DIE      UABEND SAREASON
         EJECT ,
*----------------------------------------------------------------------
*  Data areas
*----------------------------------------------------------------------
         LTORG ,
*----------------------------------------------------------------------
*  Savearea/workarea
*----------------------------------------------------------------------
SAVEAREA DSECT ,
SASA     DS    18F                     savearea for anyone we call
SAREASON DS    X                       UABEND reason code
SACTL    DS    F                       @ control segment
SABUF    DS    CL80                    message record buffer
         DS    0D
L$SA     EQU   *-SAVEAREA
*
         EJECT ,
         DRCS2 ,
*
         PUSH  PRINT
         PRINT NOGEN
         WARECV ,
         POP   PRINT
*
RECVMSG  UFINI DXDRMSG,L$SA              pseudo-register
         END   ,
