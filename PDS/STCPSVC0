STCPSVC0 TITLE 'TCP/IP SVC initialization'
*
* Copyright 2002 James M. Morrison
*
*----------------------------------------------------------------------
*  To do:
*----------------------------------------------------------------------
         U#SET FUNC=STCP
*----------------------------------------------------------------------
* Function:    Steals an SVC entry from SVCTABLE
*
* Entry:  Enabled, Supervisor state, Key 8
*
*         R0     bits 00-23  ignored
*                bits 24-31  SVC number to install
*
*         R1     address of SVC load point (= entry point)
*
* Exit:   Enabled, Supervisor state, Key 8
*
*         R15 =  0 - OK
*
* Notes:  IBM recommends user SVC numbers be assigned starting at
*         255 and working towards 200; SVCs 0-199 are reserved
*         for IBM
*
*         STCPSVC9 de-installs our SVC
*
* IBM Documentation:
*
* Order Number Title
* ------------ -----------------------------------------
*
* GC28-0628-03 OS/VS2 System Programming Library: Supervisor
*              Fourth Edition, August    1979
*
*      Miscellaneous Services - Writing SVC Routines
*
* GC26-3792-08 OS/VS2 System Programming Library:
*              System Generation Reference
*              Release 3.8
*              Ninth  Edition, October   1980
*
* Data Areas:
*
* Anchor   Area     Create Macro
* -------- -----    ------ --------
*
* CVTPTR   CVT      SYSGEN CVT       Communications Vector Table
* CVTABEND SCVT     SYSGEN IHASCVT   Secondary Comm Vector Table
* SCVTSVCT SVCTABLE SYSGEN IHASVC
*
* Areas created by SYSGEN are nucleus-resident, Key 0
*
* Source code:
*
* SYM11 IHASVC   - SVC table entry
*
* SYM11 IEAVNPS5 - SVCTABLE initialization (NIP)
*
* SYM11 IEAVESVC - SVC FLIH
*
*----------------------------------------------------------------------
ERR#1    EQU   1        SVC # not acceptable
*
STCPSVC0 UPROC ID=SS00,L=L$SA
         STM   R0,R1,SAENTRY
         LR    R8,R0                   save SVC #
         LA    R9,0(,R1)               save SVC entry point addr
*
         UPSR  1,'SVC0 SVC #',(R8)
         UPSR  1,'SVC0 SVC @',(R9)
         SLL   R8,32-8
         SRL   R8,32-8                 remove extraneous bits
         MVI   SAREASON,ERR#1          SVC # not acceptable
         C     R8,=F'200'              SVC # too low?
         BL    DIE
*
         L     R3,CVTPTR               @ CVT
         USING CVT,R3
         L     R4,CVTABEND             @ 2ndry CVT
         USING SCVTSECT,R4
         L     R5,SCVTSVCT             @ SVCTABLE
         USING SVCENTRY,R5
         DROP  R3,R4
         UDIAG 5,'SVC0 SVCTABLE pre mod',(R5),16+(256*L$SVC)
*
         LR    R10,R8
         SLL   R8,3                    SVC entry length = 8
         LA    R5,SVCENTRY(R8)         @ target SVC entry
         UDIAG 4,'SVC0 SVC old entry',(R5),L$SVC
         C     R9,SVCEP                already installed?
         BE    EXIT
*
         ST    R5,STP@SVCT             save addr of SVC entry
         MVC   STPSVCTB,SVCENTRY       save old SVC entry for restore
         SPKA  0
         ST    R9,SVCEP                overlay with SVC entry addr
         MVC   SVCATTR1(L$SVC-L'SVCEP),SVCMODEL    update SVCTABLE
         SPKA  8*16
*
         UDIAG 4,'SVC0 TCP/IP SVC new entry',(R5),L$SVC
         UDIAG 3,'SVC0 WASTCP SVC fields',STPSVCXX,L'STPSVCXX
         B     EXIT                    not ready to test SVC yet
*
*  DEBUG
*
         SLR   R0,R0
         LA    R1,1
         LA    R15,15
*
         MVI   SASVC,X'0A'             SVC opcode
         IC    R14,SAENTRY+3           grab SVC #
         STC   R14,SASVC+1
         EX    R0,SASVC                invoke SVC
         STM   R15,R1,SATEMP           save SVC return regs
         UDIAG 4,'SVC0 SVC return',SATEMP,12
*
EXIT     UPEND ,                       return to caller
         EJECT ,
*----------------------------------------------------------------------
DIE      UABEND SAREASON               bye bye
         EJECT ,
*----------------------------------------------------------------------
*  Data areas
*----------------------------------------------------------------------
SVCMODEL DC    AL1(SVCTP34,0,0,0)
         LTORG ,
SAVEAREA DSECT ,
SASA     DS    18F
SAENTRY  DS    2F                      R0, R1 from caller
SATEMP   DS    3F
SASVC    SVC   0
SAREASON DS    X
         DS    0D
L$SA     EQU   *-SAVEAREA
*
         WASTCP ,                      TCP/IP common
         CVT    DSECT=YES,LIST=NO
         IEFJESCT ,                    JESCT
         IEFJSCVT ,                    SSCVT
         IEFJSSVT ,                    SSVT
         IHASCVT ,                     secondary CVT
         IHASVC ,                      SVCTABLE entry
L$SVC    EQU   *-SVCENTRY              length of SVCTABLE entry
STCPSVC0 UFINI DXDSSVC0,L$SA
         END   ,
