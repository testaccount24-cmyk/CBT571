U370VADR TITLE 'Text description of virtual address'
*
* Copyright 2002 James M. Morrison
*
*----------------------------------------------------------------------
*  To do:
*  1) Check MVS38j SHOWMVS, finish writing CKAREA
*----------------------------------------------------------------------
         U#SET FUNC=UTIL
*----------------------------------------------------------------------
* Function:    Given a virtual address, attempt to describe that
*              address with a text message
*
*              See BUFFER DSECT for output layout
*
* Entry:  R0 = virtual address to be described
*         R1 = addr of CL40 field to receive text
*              DVADRBUF macro describes this area
*
* Exit:   R15 = 00
*
* Diagnostics:
*             3 - CDE/XTLST
*             9 - entry/exit
*
* Notes:
*
*----------------------------------------------------------------------
* Reg  DSECT    usage
*----------------------------------------------------------------------
* R2            vaddr caller wants described
* R3   BUFFER   output buffer
* R4
* R5
* R6
* R7
* R8
* R9            internal linkage
* R10           internal linkage
* R11  TGD      common
* R12           base reg
* R13  SAVEAREA savearea
*----------------------------------------------------------------------
         TITLE ' '
U370VADR UPROC ID=UVAD,L=L$SA,CPD=(N,0)
         USING PSA,0
         LR    R2,R0                   requested virtual addr
         LA    R2,0(,R2)               clean hi byte
         LR    R3,R1                   @ output buffer
         USING BUFFER,R3
*
         UDIAG 9,'U370VADR enter'
*
         MVI   0(R3),C' '
         MVC   1(L$VADRBF-1,R3),0(R3)  clear callers' buffer
         EJECT ,
*----------------------------------------------------------------------
*  Check areas first, refine search when found in specific area
*----------------------------------------------------------------------
CKAREA   DS    0H
*
CKPSA    SLR   R14,R14                 PSA begins at location 0
         L     R15,=A(L$PSA)           PSA length = 4KB
         LA    R9,CKNUC                CKBOUND failure addr
         BAL   R10,CKBOUND             vaddr within area?
         MVC   BUFAREA(3),=C'PSA'
         B     AREAHIT
*
CKNUC    L     R4,FLCCVT               @ CVT
         USING CVT,R4
         L     R14,=A(L$PSA)           PSA/NUC contiguous
         L     R15,CVTNUCB             lowest addr not in nuc
         BCTR  R15,0                   lowest nuc addr
         SR    R15,R14                 convert to length for CKBOUND
         LA    R9,CKPRIV               CKBOUND failure addr
         BAL   R10,CKBOUND             vaddr within area?
         MVC   BUFAREA(3),=C'NUC'
         B     AREAHIT
*
CKPRIV   L     R5,CVTGDA               @ Global Data Area
         USING GDA,R5
         L     R14,PASTRT              GDA -> private area start
         L     R15,PASIZE              GDA -> private area length
         LA    R9,CKSQA                CKBOUND failure addr
         BAL   R10,CKBOUND             vaddr within area?
         MVC   BUFAREA(4),=C'PRIV'
         B     SCANJPAK                refine search further
*
CKSQA    L     R6,SQASPQEP             GDA -> SQA (SP245) SPQE
         USING SPQESECT,R6
         L     R7,SPDQEPTR             SPQE -> DQE
         USING DQESECT,R7
         L     R14,DQEBLKAD            @ SQA begin
         L     R15,DQELNTH             SQA length
         LA    R9,CKPLPA               CKBOUND failure addr
         BAL   R10,CKBOUND             vaddr within area?
         MVC   BUFAREA(3),=C'SQA'
         B     AREAHIT
*
CKPLPA   LR    R15,R14                 @ SQA begin
         BCTR  R15,0                   PLPA/SQA contiguous
         L     R14,CVTLPDIA            CVT -> LPA directory
         LA    R14,0(,R14)             clear hi byte
         SR    R15,R14                 convert to length for CKBOUND
         LA    R9,CKCSA                CKBOUND failure addr
         BAL   R10,CKBOUND             vaddr within area?
         MVC   BUFAREA(4),=C'PLPA'
         B     SCANLPA                 refine search further
*
CKCSA    L     R6,CSAPQEP              GDA -> CSA PQE
         USING PQESECT,R6
         L     R14,PQEREGN             @ CSA
         L     R15,PQESIZE             L' CSA
         LA    R9,COMMON               CKBOUND failure addr
         BAL   R10,CKBOUND             vaddr within area?
         MVC   BUFAREA(3),=C'CSA'
         B     AREAHIT
*
*  OK, it's not PSA, NUC, or PRIV ... hence it's somewhere in COMMON
*
COMMON   L     R15,CVTMZ00             top of machine
         MVC   BUFAREA(6),=C'COMMON'
*
AREAHIT  DS    0H
         BAL   R10,FMTAREA             stuff R14/R15 into message
         B     EXIT
         EJECT ,
*----------------------------------------------------------------------
*  Scan Link Pack (common area)
*----------------------------------------------------------------------
SCANLPA  DS    0H
         L     R4,FLCCVT               @ CVT
         USING CVT,R4
         L     R5,CVTQLPAQ             @ 1st LPDE
         ICM   R5,15,0(R5)             anybody home?
         BZ    EXIT
         USING LPDE,R5
         BAL   R9,SCLPLOOP             R9 @ CKBOUND failure addr
*
SCLPNEXT ICM   R5,15,LPDECHN           next LPDE
         BZ    EXIT                    no more LPDEs
*
SCLPLOOP DS    0H
         TM    LPDEATTR,LPDEMIN        minor LPDE?
         BOR   R9                      yes, ignore it
         L     R14,LPDEXTAD            addr of module load pt
         L     R15,LPDEXTLN            length of module in vstor
         BAL   R10,CKBOUND             check R2 within bounds
*
*  Found LPDE containing vaddr
*
         MVC   BUFAREA(3),=C'LPA'
         MVC   BUFMOD,LPDENAME         module name
         BAL   R10,FMTAREA             stuff R14/R15 into message
         B     SCANVTAB                see if one of our CSECTs
         EJECT ,
*----------------------------------------------------------------------
*  Scan CDEs in TCB's Job Pack (private area)
*----------------------------------------------------------------------
SCANJPAK DS    0H
         L     R4,PSATOLD              current TCB
         USING TCB,R4
         L     R4,TCBJSTCB             job step TCB
         L     R5,TCBJPQ               @ 1st jobpack CDE
         USING CDENTRY,R5
         BAL   R9,JPCDLOOP             @ CKBOUND failure addr
*
JPCDNEXT ICM   R5,15,CDCHAIN           next CDE
         BZ    PRIVMORE                check more private area stuff
*
JPCDLOOP DS    0H
*        UDIAG 3,'U370VADR CDE',(R5),L$CDE
         TM    CDATTR,CDMIN            minor CDE?
         BOR   R9                      yes, skip it
*
         L     R6,CDXLMJP              extent list
         USING XTLST,R6
*        UDIAG 3,'U370VADR XTLST',(R6),L$XTLST
*
         L     R14,XTLMSBAA            addr of module load pt
         SLR   R15,R15
         ICM   R15,7,XTLMSBLN          length of module in vstor
         BAL   R10,CKBOUND             R2 within bounds?
*
*  Found CDE containing vaddr
*
         MVC   BUFAREA(4),=C'JPAQ'
         MVC   BUFMOD,CDNAME           just right, have module name
         BAL   R10,FMTAREA             stuff R14/R15 into message
         DROP  R4,R5,R6
         EJECT ,
*----------------------------------------------------------------------
*  Scan VCON table (vaddr found in either LPA or JobPack)
*----------------------------------------------------------------------
SCANVTAB DS    0H
         ICM   R15,15,TGD@VTAB         @ VCON table
         BZ    EXIT                    no VCON table
         LM    R5,R7,0(R15)
         USING VTABLE,R5
*
         LA    R10,SVCSECT             @ CKBOUND success addr
SVLOOP   DS    0H
         L     R14,VTABVCON            @ CSECT begin
         LH    R15,VTCSL               approx CSECT length
         BAL   R9,CKBOUND              see if within bounds
         BXLE  R5,R6,SVLOOP
         B     EXIT
*
SVCSECT  DS    0H
         MVI   BUFDOT1,C'.'
         MVC   BUFCSECT,VTABNAME       CSECT found in VCON table
         MVI   BUFDOT2,C'+'
         LR    R0,R2
         SR    R0,R14                  offset within CSECT
         LA    R1,BUFOFF
         UCALL U370HEX
         B     EXIT
         EJECT ,
*----------------------------------------------------------------------
*  Private area - not in CDE
*----------------------------------------------------------------------
PRIVMORE DS    0H
         CR    R2,WAREG                TGD?
         BNE   PRIV002
         MVC   BUFMOD(3),=C'TGD'
         LR    R14,WAREG
         LA    R15,L$TGD
         AR    R15,R14
         BAL   R10,FMTAREA
         B     EXIT
*
*  Scan VCON table for CPD & DSA
*
PRIV002  DS    0H
         ICM   R15,15,TGD@VTAB         @ VCON table
         BZ    PRIV003                 no VCON table
         LM    R5,R7,0(R15)
         USING VTABLE,R5
*
CPLOOP   DS    0H
         L     R14,VT@CPD              @ CSECT's CPD
         LH    R15,VTCPDL              CPD length
         LA    R10,CPHIT               @ CKBOUND success addr
         BAL   R9,CKBOUND              see if within bounds
*
         L     R14,VT@SX               @ CSECT's SX
         LH    R15,VTDSAL              DSA length
         LA    R10,SXHIT               @ CKBOUND success addr
         BAL   R9,CKBOUND              see if within bounds
*
         BXLE  R5,R6,CPLOOP
         B     PRIV003
*
CPHIT    DS    0H
         MVC   BUFMOD(3),=C'CPD'
         B     SVWHO
*
SXHIT    DS    0H
         MVC   BUFMOD(3),=C'DSA'
*
SVWHO    DS    0H
         MVI   BUFDOT1,C' '
         MVC   BUFCSECT,VTABNAME       CSECT found in VCON table
         MVI   BUFDOT2,C' '
         B     EXIT
*
PRIV003  DS    0H
         LR    R14,WAREG               DSA pool?
         L     R15,TGDLDSA
         LA    R9,PRIV004
         BAL   R10,CKBOUND
         MVC   BUFMOD,=C'DSA POOL'
         BAL   R10,FMTAREA             stuff R14/R15 into message
         B     EXIT
*
PRIV004  DS    0H
PRIV999  DS    0H
         L     R4,FLCCVT               @ CVT
         USING CVT,R4
         L     R5,CVTGDA               @ Global Data Area
         USING GDA,R5
         L     R14,PASTRT              GDA -> private area start
         L     R15,PASIZE              GDA -> private area length
         AR    R15,R14
         BAL   R10,FMTAREA
         EJECT ,
*----------------------------------------------------------------------
*  Return to caller
*----------------------------------------------------------------------
EXIT     DS    0H
         UDIAG 9,'U370VADR exit'
         UPEND ,                       return to caller
         LTORG ,
         EJECT ,
*----------------------------------------------------------------------
*  CKBOUND - check boundaries - R2 vaddr, R14 begin addr, R15 length
*  Returns via R9 for failure, R10 for success
*----------------------------------------------------------------------
CKBOUND  DS    0H
         LA    R14,0(,R14)             clean hi byte
         CR    R2,R14                  vaddr too low?
         BLR   R9                      yes, fail return
         AR    R15,R14                 @ past end of module
         CR    R2,R15                  vaddr too high?
         BNLR  R9                      yes, fail return
         ST    R14,SA@MOD              save module load point addr
         BR    R10                     no, success return
*----------------------------------------------------------------------
*  FMTAREA - Given R14 & R15 (after CKBOUND success), fill in
*  description of area containing vaddr
*----------------------------------------------------------------------
FMTAREA  DS    0H
         STM   R14,R15,SAAREA          save @start, @end
         LR    R0,R14
         LA    R1,BUF@BEG
         UCALL U370HEX
         MVI   BUFDASH,C'-'
         L     R0,SAAREA+4             @ area end
         LA    R1,BUF@END
         UCALL U370HEX
         BR    R10                     no, success return
*----------------------------------------------------------------------
*  BUFFER description
*----------------------------------------------------------------------
BUFFER   DSECT ,
         DVADRBUF PFX=BUF
*----------------------------------------------------------------------
*  Dynamic savearea & workarea
*----------------------------------------------------------------------
SAVEAREA DSECT ,
SASA     DS    18F
SA@MOD   DS    A                       addr of module containg vaddr
SAAREA   DS    2A                      FMTAREA @start, @end
         DS    0D
L$SA     EQU   *-SAVEAREA              length of savearea
*
         UVTAB MF=DSECT                VCON table
         WATGD ,
*
         PRINT OFF,NOGEN
         IHAPSA DSECT=YES
L$PSA    EQU   *-PSA
         CVT   DSECT=YES,LIST=NO
         IKJTCB DSECT=YES,LIST=NO
         IHALPDE ,
         IHACDE ,
L$CDE    EQU   *-CDENTRY
         IHALLE ,
         IHAXTLST ,
L$XTLST  EQU   *-XTLST
*
         EJECT ,
         IHAGDA ,
         IHAPQE ,
         IHASPQE ,
         IHADQE ,
         PRINT GEN
U370VADR UFINI DXDUVADR,L$SA
         END   ,
