U370HEX  TITLE 'Convert hex value in R0 into displayable hex'
*
* Copyright 2001, 2002 James M. Morrison
*
*----------------------------------------------------------------------
         U#SET FUNC=UTIL
*----------------------------------------------------------------------
* Function:    converts hex value in R0 into displayable hex
*
* Entry:  R0 = contents of R0 to be converted & displayed
*         R1 = 0 or address of CL8 field to receive output
*
* Exit:   R15 =  0     everything is OK
*         R0  =  1st word of output
*         R1  =  2nd word of output
*
* Notes:  U370HEX is called by UTILABND, and is neither allowed to have
*         errors in its code, nor to issue ABEND/UABEND.
*
*----------------------------------------------------------------------
U370HEX  UPROC ID=UHEX,L=L$SAV
*
         ST    R0,WKFWORD              value to translate
         UNPK  WKWORK,WKFWORD(5)       spread out digits
         LA    R15,HEXCHAR             addressability game
         LA    R14,C'0'
         SR    R15,R14
         TR    WKWORK,0(R15)           hexchar-c'0' translate to char
         LTR   R1,R1                   caller supplied field addr?
         BZ    RETREGS                 no, return regs only
*
         MVC   0(8,R1),WKWORK          copy input msg to caller's buf
*
RETREGS  LM    R0,R1,WKWORK            return values
*
         UPEND R0=PASS,R1=PASS,RC=0    return to caller
*
         LTORG ,
*
HEXCHAR  DC    C'0123456789ABCDEF'     Hex translate table
*----------------------------------------------------------------------
*  Dynamic savearea & workarea
*----------------------------------------------------------------------
SAVEAREA DSECT ,
SAVREGS  DS    18F
*
WKFWORD  DS    F,C
*
         DS    0F
WKWORK   DS    CL9                     8 bytes plus trailing slop byte
*
         DS    0D                      align
L$SAV    EQU   *-SAVEAREA              length of savearea
*
         PRINT NOGEN
         WATGD ,
         PRINT GEN
U370HEX  UFINI DXDUHEX,L$SAV
         END   ,
