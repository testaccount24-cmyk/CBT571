XMITESZ  TITLE 'Estimate dataset SiZe'
*
* Copyright 2001, 2002 James M. Morrison
*
*----------------------------------------------------------------------
*  To do: YES
*  1) Rather than running through the DEB for _allocated_ tracks,
*     use the DS1LSTAR field of the Format1 DSCB
*----------------------------------------------------------------------
         U#SET FUNC=XMIT
*----------------------------------------------------------------------
* Function: Estimate the number of bytes allocated to a dataset
*
* Caller: XMIT02A
*
* Entry:  AMODE=24, KEY=8, STATE=supervisor
*
*         R0  =  Format 1 DSCB
*         R1  =  open DCB
*
* Exit:   R15 = return code
*         R15 = 00 success; R0 = # bytes allocated (approximately)
*
* Abends: yes
*
* Notes:
*----------------------------------------------------------------------
* R2     scratch
* R3     DVCT
* R4     UCB associated with extent
* R5     # extents
* R6     DCB
* R7     DEB, DEBDASD
* R8     estimated file size in bytes
* R9     Format1 DSCB
* R10
* R11    XMTWA
* R12    base reg
* R13    savearea
*----------------------------------------------------------------------
ERR#1    EQU   1        dataset not on dasd device
         TITLE ' '
*----------------------------------------------------------------------
XMITESZ  UPROC L=L$SA,ID=XESZ
         LR    R9,R0                   Format1 DSCB
         LR    R6,R1                   SYSUT1/SYSUT2 open DCB
         USING IHADCB,R6
         UPSR  1,'ESZ enter',(R6)
*
*  Calculate estimated file size
*
         SLR   R8,R8                   # bytes allocated (estimate)
         SLR   R7,R7
         ICM   R7,7,DCBDEBA            @ DEB for DCB
         USING DEBBASIC,R7
*
         LR    R1,R7
         S     R1,=A(DEBPREFX-DEBBASIC)     back up to prefix
         SLR   R2,R2
         IC    R2,DEBLNGTH-DEBPREFX(,R1)    L' DEB in doublewords
         SLL   R2,3                         L' DEB in bytes
         SPKA  0                            DEB in key 5
         UDIAG 3,'ESZ DEB',(R7),(R2)
*
         SLR   R5,R5
         IC    R5,DEBNMEXT                  # extents
         LTR   R15,R5
         BZ    EXTDONE                      no extents
         UPSR  4,'ESZ extents',(R5)
         LA    R7,DEBBASND                  @ deb dasd section
         USING DEBDASD,R7
         UPSR  4,'ESZ DEB dasd section',(R7)
*
* since we don't support ISAM, we don't have to worry about the
* ISAM device dependent section possibly appearing between the
* deb basic section and the deb dasd section.
*
CALCSPAC DS    0H
         SLR   R4,R4
         ICM   R4,7,DEBUCBA                 @ UCB for extent
         USING UCB,R4
         TM    UCBTBYT3,UCB3DACC            dasd device?
         BO    DASD
         MVI   SAREASON,ERR#1               not dasd device
DIE      UABEND ,                           input device not dasd
DASD     DS    0H
         L     R1,CVTPTR                    @ cvt
         L     R1,CVTZDTAB-CVT(,R1)         @ ieczdtab
         USING DVCTI,R1
         LA    R2,DVCTYPMK                  4 low order bits mask
         N     R2,UCBTYP                    index into IECZDTAB
         IC    R2,DVCTIOFF(R2)              offset to DVCT entry
         LA    R3,DVCTI(R2)                 @ DVCT entry
         USING DVCT,R3
         UDIAG 4,'XMITESZ DVCT entry',(R3),DVCENTLG
         DROP  R1
*
         UDIAG 3,'XMITESZ DEBDASD',(R7),L$DEBDA
*
         SLR   R2,R2
         ICM   R2,3,DEBNMTRK                tracks allocated in extent
         UPSR  2,'ESZ tracks',(R2)
         SLR   R0,R0
         LR    R1,R2
         SLR   R14,R14
         ICM   R14,3,DVCTRKLN               # bytes/track
         MR    R0,R14                       = bytes in extent
         AR    R8,R1                        accumulate bytes allocated
         UPSR  2,'ESZ bytes',(R8)
         LA    R7,L$DEBDA(,R7)              next debdasd section
         BCT   R5,CALCSPAC                  look at all extents
         SLR   R15,R15                      good return code
*
EXTDONE  DS    0H
         SPKA  8*16                         back to key 8
         ST    R15,SARC
         UPSR  1,'ESZ exit; size',(R8)
         LR    R0,R8                        return estimated size
         UPEND R0=PASS,RC=*SARC
*----------------------------------------------------------------------
*  Data areas
*----------------------------------------------------------------------
         LTORG ,
SAVEAREA DSECT ,
SASA     DS    18F
SAENTRY  DS    2F
SARC     DS    F             return code
SAREASON DS    X
         DS    0D
L$SA     EQU   *-SAVEAREA
*
         EJECT ,
         PRINT NOGEN
         WAXMIT ,
         IEZDEB LIST=NO
L$DEBDA  EQU   (DEBNMTRK+L'DEBNMTRK)-DEBDASD     l' debdasd section
UCB      DSECT ,
         IEFUCBOB LIST=NO
DSCB1    DSECT ,
         IECSDSL1 (1)                            Format 1 DSCB
*----------------------------------------------------------------------
*  See Stage2 assembly of IECZDTAB for values & index
*----------------------------------------------------------------------
         IHADVCT ,
         PRINT NOGEN
         CVT   DSECT=YES,LIST=NO
         DCBD  DSORG=PS,DEVD=DA
XMITESZ  UFINI DXDXESZ,L$SA
         END   ,
