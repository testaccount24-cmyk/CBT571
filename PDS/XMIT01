XMIT01   TITLE 'WRITE INMR01 segment'
*
* Copyright 2001, 2002 James M. Morrison
*
*----------------------------------------------------------------------
*  To do:
*----------------------------------------------------------------------
         U#SET FUNC=XMIT
*----------------------------------------------------------------------
* Function: Collect the information for, and write the INMR01 segment.
*
* Caller: XMITUNLD
*
* Entry:  AMODE=24, ASC=PRIMARY, KEY=*, STATE=*
*
*         R0  =
*         R1  =
*
* Exit:   R15 = 00 = return code
*
* Notes:
*----------------------------------------------------------------------
* R2
* R3
* R4     @ segment data
* R5     length remaining in segment data
* R6
* R7
* R8
* R9
* R10
* R11    XMTWA
* R12    base reg
* R13    savearea
*----------------------------------------------------------------------
         TITLE ' '
*----------------------------------------------------------------------
XMIT01   UPROC L=L$SA,ID=XM01,INIT=MVCL
         UDIAG 1,'XMIT01 enter'
*----------------------------------------------------------------------
* Create INMR01 segment's key data
*----------------------------------------------------------------------
         MVI   X01SEGL,8                    segment length
         MVI   X01SEGF,SEG$1+SEG$L+SEG$C    segment flag
         MVC   X01ID,=C'INMR01'
*----------------------------------------------------------------------
* Update key data
*----------------------------------------------------------------------
         MVC   SAKEY1(L$PROT),PROTLIST      copy prototype list
         TIME  DEC                          R0=HHMMSSth, R1=00YYDDDF
         STM   R0,R1,SATIMED
         SLR   R0,R0                        Julian to Gregorian
         LA    R1,SATIMED
         UCALL U370DATE                     convert date, time
         MVC   SAKFTIME,KFTIME              set date, time key info
         LA    R0,SAKFTIME
         ST    R0,SA@FTIME                  provide U370DATE key ptr
*----------------------------------------------------------------------
*  Build INMR01 segment
*----------------------------------------------------------------------
BUILD    DS    0H
         LA    R0,X01MR01                   @ segment buffer
         ST    R0,SA@BUF
         OI    SAKEYX,X'80'                 set VL flag
         LA    R1,SAKEYPL                   @ parmlist
         UCALL XMITKEY                      build segment from key list
*----------------------------------------------------------------------
*  Write INMR01 segment
*----------------------------------------------------------------------
WRITE    DS    0H
         LA    R1,X01MR01
         UCALL XMITPUT                      write INMR01 segment
EXIT     DS    0H
         UDIAG 1,'XMIT01 exited'
         SLR   R15,R15                      good return code
         UPEND  ,
         EJECT ,
*----------------------------------------------------------------------
*  Data areas
*----------------------------------------------------------------------
         LTORG ,
*
MR01     DS    0X
KLRECL   DC    AL2(INMLRECL,1,L'VLRECL)    0042 physical lrecl
VLRECL   DC    FL1'80'                     of control segments
*
KFNODE   DC    AL2(INMFNODE,1,L'VFNODE)    1011 origin node
VFNODE   DC    C'ORIGNODE'
*
KFUID    DC    AL2(INMFUID,1,L'VFUID)      1012 origin userid
VFUID    DC    C'ORIGUID'
*
KTNODE   DC    AL2(INMTNODE,1,L'VTNODE)    1001 dest node
VTNODE   DC    C'DESTNODE'
*
KTUID    DC    AL2(INMTUID,1,L'VTUID)      1002 dest userid
VTUID    DC    C'DESTUID'
*
KFTIME   DC    AL2(INMFTIME,1,14)          1024 origin time stamp
L$KFTIME EQU   *-KFTIME
*
KNUMF    DC    AL2(INMNUMF,1,L'VNUMF)      102F # files
VNUMF    DC    X'01'
*
* optional keys not provided: INMFACK, INMFVERS, INMNUMF, INMUSERP
*
*----------------------------------------------------------------------
PROTLIST DS    0D            prototype key list passed to U370KEY
X1@LRECL DC    A(KLRECL)     addr of LRECL key info
X1@FNODE DC    A(KFNODE)             ORIGIN NODE
X1@FUID  DC    A(KFUID)              ORIGIN USERID
X1@TNODE DC    A(KTNODE)             DEST NODE
X1@TUID  DC    A(KTUID)              DEST USERID
X1@FTIME DC    A(KFTIME)             ORIGIN TIME STAMP
X1@NUMF  DC    A(KNUMF)              NUMBER OF FILES
L$PROT   EQU   *-PROTLIST
*----------------------------------------------------------------------
*  Workarea
*----------------------------------------------------------------------
SAVEAREA DSECT ,
SASA     DS    18F
*
SATIMED  DS    D                       TIME DEC R0:R1
SAKFTIME EQU   SATIMED+2,6
SATIMEE  DS    CL16'YYYYMMDDHHMMSSTH'  U370DATE return buffer
*
SAKEYPL  DS    0D            XMITKEY parmlist
SA@BUF   DS    A             addr of seg buffer
SAKEY1   EQU   *             beginning of key addresses
SA@LRECL DS    A             addr of LRECL key info
SA@FNODE DS    A                     ORIGIN NODE
SA@FUID  DS    A                     ORIGIN USERID
SA@TNODE DS    A                     DEST NODE
SA@TUID  DS    A                     DEST USERID
SA@FTIME DS    A                     ORIGIN TIME STAMP
SA@NUMF  DS    A                     NUMBER OF FILES
SAKEYX   EQU   *-4,1         VL byte
*
X01MR01  DS    0D            INMR01 segment
X01SEGL  DS    X             segment length
X01SEGF  DS    X             segment flag
X01SEGD  DS    XL253         segment data
X01ID    EQU   X01SEGD,6     C'INMR01'
         DS    0D
L$SA     EQU   *-SAVEAREA
         PRINT NOGEN
         DXSEG ,
         WAXMIT ,
XMIT01   UFINI DXDX01,L$SA           pseudo-register
         END   ,
