U370LOG  TITLE 'LOG output line to xxxxLOG ddname'
*
* Copyright 2001, 2002 James M. Morrison
*
*----------------------------------------------------------------------
*  Things to do:
*----------------------------------------------------------------------
         U#SET FUNC=UTIL
*----------------------------------------------------------------------
* Function: Write output line to xxxxLOG ddname
*
* Entry:  AMODE=24, ASC=PRIMARY, KEY=*, STATE=*
*
*         R0  =  text length
*         R1  =  Address of text to be written
*                when 0, xxxxLOG DCB will be closed.
*
*         TGDLOGD = xxxxLOG ddname
*         If x'00', default is to build from appl name + 'LOG '
*
* Exit:   R15 = 0
*
* Abends: U0001 DCB didn't open
*
* Notes:  U370LOG maintains the xxxxLOG DCB, opening it as required.
*----------------------------------------------------------------------
* R2
* R3     DCB
* R4     input buffer
* R5     length of input buffer
* R6
* R7
* R8
* R9     CPD
* R10
* R11    TGDWA
* R12    base reg
* R13    savearea
*----------------------------------------------------------------------
JUNK     DSECT ,
JUNKDCB  DCB   DDNAME=XXXXLOG,                                         +
               MACRF=PM,DSORG=PS,RECFM=VBA,                            +
               LRECL=133+4,BLKSIZE=6144
L$DCB    EQU   *-JUNKDCB
*----------------------------------------------------------------------
*  TPU - CPD user additions
*----------------------------------------------------------------------
TPU      DSECT ,                  task persistent data - user section
TPUDCB   DS    XL(L$DCB)          xxxxLOG DCB
L$TPU    EQU   *-TPU              length of user additions
*----------------------------------------------------------------------
         TITLE ' '
*----------------------------------------------------------------------
U370LOG  UPROC ID=ULOG,L=L$SA,CPD=(Y,L$TPU)
         LR    R4,R0                        save text length
         LR    R5,R1                        save text addr
         U@CPD TPU,R9,ERR=NOCPD             locate CPD w/ DCB in it
         LA    R3,TPUDCB                    @ DCB in CPD
         USING IHADCB,R3
*
         LTR   R5,R5                        @ text, or CLOSE request
         BZ    CLOSE
*
*  If the DCB hasn't been moved to the CPD yet, the next test is ok,
*  although instead of testing the DCB it's testing x'00's
*
         TM    DCBOFLG1,DCBOFOPN            dcb open?
         BO    NORMAL
         EJECT ,
*----------------------------------------------------------------------
*  Initialize CPD with our DCB, open it
*----------------------------------------------------------------------
         MVC   SAOPN(L$OPN),MODLOPN         copy model OPEN
         MVC   TPUDCB(L$DCB),MODLDCB        copy model DCB macro
         MVC   DCBDDNAM(4),TGDEYE           copy appl prefix
         CLI   TGDLOGD,0                    alternate DDname?
         BE    *+10
         MVC   DCBDDNAM,TGDLOGD             copy alternate DDname
         OPEN  TPUDCB,MF=(E,SAOPN)
         TM    DCBOFLG1,DCBOFOPN            opened?
         BO    NORMAL
         WTO   'U370LOG: xxxxLOG ddname did not open'
         UABEND ,                           output DCB didn't open
*----------------------------------------------------------------------
*  Close xxxxLOG
*----------------------------------------------------------------------
CLOSE    DS    0H
         TM    DCBOFLG1,DCBOFOPN            xxxxLOG open?
         BNO   EXITRC0
         MVC   SACLOS(L$CLOS),MODLCLOS      copy model CLOSE macro
         CLOSE TPUDCB,MF=(E,SACLOS)         close xxxxLOG
         FREEPOOL TPUDCB
         B     EXITRC0
*----------------------------------------------------------------------
*  Normal write request
*----------------------------------------------------------------------
NORMAL   DS    0H
         LA    R0,L'SAPFX+L'SARESV+L'SACC
         AR    R4,R0
         STH   R4,SAPFX                     set VB file's RDW
         SR    R4,R0
         MVI   SACC,C' '                    set carriage control
         BCTR  R4,0
         EX    R4,COPY                      copy record to buffer
         LA    R0,SAPFX                     record address
         PUT   TPUDCB                       write record
*
EXITRC0  SLR   R15,R15                      set good return code
EXIT     UPEND ,
*
NOCPD    WTO   'U370LOG no CPD'
         UABEND =X'FF'
*
COPY     MVC   SABUF(*-*),0(R5)             executed
*
MODLOPN  OPEN  (,OUTPUT),MF=L          model OPEN
L$OPN    EQU   *-MODLOPN               l' model open
*
MODLCLOS CLOSE (,),MF=L                model CLOSE
L$CLOS   EQU   *-MODLCLOS              l' model close
*
MODLDCB  DCB   DDNAME=XXXXLOG,                                         +
               MACRF=PM,DSORG=PS,RECFM=VBA,                            +
               LRECL=133+4,BLKSIZE=6144
*
         LTORG ,
*----------------------------------------------------------------------
SAVEAREA DSECT ,
         DS    18F
SAOPN    OPEN  (,),MF=L
SACLOS   CLOSE (,),MF=L
         DS    0F
SAPFX    DS    H                       rdw length (includes l' rdw)
SARESV   DS    H                       rdw reserved
SACC     DS    C' '                    asa carriage control
SABUF    DS    CL133                   output record
         DS    0D
L$SA     EQU *-SAVEAREA
         EJECT ,
*
         WATGD ,
         PRINT NOGEN
         DCBD  DSORG=PS,DEVD=DA
         PRINT GEN
U370LOG  UFINI DXDULOG,L$SA
         END   ,
