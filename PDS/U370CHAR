U370CHAR TITLE 'Convert R0 into displayable hex'
*
* Copyright 2002 James M. Morrison
*
*----------------------------------------------------------------------
         U#SET FUNC=UTIL
*----------------------------------------------------------------------
* Function:    converts character string to hex
*
* Entry:  R0(00:00)    flag bit - hex chars allowed (CHAR$HEX)
*         R0(24:31)    length of string
*         R1           @ char string to be converted to hex
*
* Exit:   R15 =  0     everything is OK
*                      R0  =  hex value
*
*                4     conversion error
*
* Diagnostics:
*              1 - RETRY
*              2 - output value
*
* Notes:
*
*----------------------------------------------------------------------
MAXDEC   EQU   10                      max decimal: 2,147,483,647
MAXHEX   EQU   6                       FFFFFF
ERR#1    EQU   1                       unable to locate TPU
RC4      EQU   4                       return code: error
*
TPU      DSECT ,
TPUESTAE DS    X                       ESTAE established flag
         DS    0D
L$TPU    EQU   *-TPU
*----------------------------------------------------------------------
U370CHAR UPROC ID=UCHR,L=L$SA,CPD=(Y,L$TPU,ESTAE)
         STM   R0,R1,SAENTRY           for RETRY routine
         LR    R5,R0                   flag + length
         LR    R6,R1                   string addr
         SLR   R7,R7                   value to return
         MVI   SARC+3,RC4              assume error rc
*
*  Establish ESTAE first time we're called (good rest of task life)
*
         BAL   R10,FINDTPU
         USING TPU,R9
         CLI   TPUESTAE,0              ESTAE set on prior call?
         BNE   HVESTAE
         UESTAE RETRY
         MVI   TPUESTAE,1              ESTAE established
HVESTAE  DS    0H
         DROP  R9
*
         SLR   R4,R4                   will contain CHAR$HEX flag
         SLDL  R4,1                    save flag in R4
         SRL   R5,1                    discard flag from R5
         LTR   R4,R4                   hex digits or decimal digits?
         BNZ   HEX
*
*  Convert decimal char string to hex
*
CHAR     DS    0H
         LA    R14,MAXDEC
         CR    R5,R14                  more than max # chars allowed?
         BH    EXIT
         BCTR  R5,0
         EX    R5,PACK                 char -> packed
         CVB   R7,SADEC                packed -> hex
         B     EXIT
*
PACK     PACK  SADEC,0(*-*,R6)         char -> packed
*
*  Convert hex char string to hex
*
HEX      DS    0H
         LA    R14,MAXHEX
         CR    R5,R14                  more than max # chars allowed?
         BH    EXIT
         SLR   R14,R14                 hex value
         SLR   R1,R1
HEXLOOP  DS    0H
         CLI   0(R6),C'9'
         BH    EXIT
         CLI   0(R6),C'0'
         BL    HEXHEX
*
HEXDEC   DS    0H                      handle 0-9
         ICM   R15,8,0(R6)
         SLL   R15,4
         SLDL  R14,4
         B     HEXNEXT
*
HEXHEX   DS    0H                      handle a-f, A-F
         IC    R1,0(R6)                grab char
         CLI   0(R6),C'A'
         BL    HEXHEXLC
         CLI   0(R6),C'F'
         BH    EXIT
HEXHEXUC LA    R15,HEXTAB-C'A'(R1)     upper case; @ char hex value
         ICM   R15,8,0(R15)
         SLL   R15,4
         SLDL  R14,4
         B     HEXNEXT
HEXHEXLC DS    0H                      lower case
         CLI   0(R6),C'a'
         BL    EXIT
         CLI   0(R6),C'f'
         BH    EXIT
         LA    R15,HEXTAB-C'a'(R1)     @ hex value for char
         ICM   R15,8,0(R15)
         SLL   R15,4
         SLDL  R14,4
*
HEXNEXT  LA    R6,1(,R6)               @ next char
         BCT   R5,HEXLOOP
         LR    R7,R14                  return value
*
EXIT     DS    0H
         UDIAG 2,'CHAR value',(R7)
         LR    R0,R7
         UPEND R0=PASS,RC=SARC         return to caller
*
FINDTPU  U@CPD TPU,R9,ERR=NOTPU        find our TPU
         BR    R10                     return w/ TPU
NOTPU    MVI   SAREASON,ERR#1          couldn't find TPU
DIE      UABEND SAREASON
*
*  Retry routine - regs are from call when ESTAE was established
*
RETRY    DS    0H
         SLR   R5,R5
         IC    R5,SAR0B3               length from entry
         L     R6,SAR1                 addr of string
         UDIAG 1,'CHAR RETRY',(R6),(R5)
         SLR   R7,R7                   return value = 0
         B     EXIT
         LTORG ,
*
HEXTAB   DC    X'0A0B0C0D0E0F'
*----------------------------------------------------------------------
*  Dynamic savearea & workarea
*----------------------------------------------------------------------
SAVEAREA DSECT ,
SAVREGS  DS    18F
SAENTRY  DS    2F                      entry regs
SAR0B0   EQU   SAENTRY+00,1            hi byte of R0 = flag
SAR0B3   EQU   SAENTRY+03,1            length of request
SAR1     EQU   SAENTRY+04,4            addr of request
SADEC    DS    PL8                     packed number
SARC     DS    F                       return code
SAREASON DS    X                       reason for ABENDing
         DS    0D                      align
L$SA     EQU   *-SAVEAREA              length of savearea
*
         PRINT NOGEN
         WATGD ,
         PRINT GEN
U370CHAR UFINI DXDUCHR,L$SA
         END   ,
