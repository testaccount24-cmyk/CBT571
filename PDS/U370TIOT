*
* Copyright 2001, 2002 James M. Morrison
*
         PRINT  NOGEN
         IHAPSA DSECT=YES              PREFIXED STORAGE AREA
*
         IKJTCB DSECT=YES,LIST=NO      TASK CONTROL BLOCK
*
TIOT     DSECT
         IEFTIOT1                      TASK INPUT-OUTPUT TABLE
*
         PRINT GEN
U370TIOT TITLE 'Scan TIOT for DDNAME'
U370TIOT CSECT ,
*----------------------------------------------------------------------
*
*  U370TIOT is called before U370DBUG has finished setting up the
*  debug bytes, so U370TIOT's message(s) always appear.
*  Not really very useful, and mildly annoying.  The DIAG=NO
*  suppresses expansion of XMITDIAG in this CSECT.
*
         U#SET DIAG=NO,FUNC=UTIL       suppress UDIAG
*----------------------------------------------------------------------
* Function:    Given a DDNAME, determine whether it is allocated
*              by scanning the TIOT.
*
* Entry:  R0 =
*         R1 = address of CL8'ddname' field
*
* Exit:   R1  =  address of TIOT entry
*                0     DDNAME not allocated
*
* Notes:
*
*----------------------------------------------------------------------
         TITLE ' '
*----------------------------------------------------------------------
* R2     TIOT entry
* R3
* R4
* R5
* R6
* R7
* R8
* R9     DDNAME ptr
* R10
*----------------------------------------------------------------------
U370TIOT UPROC ID=UTIO,L=L$SA
         LR    R9,R1                        save caller's R1
         UDIAG 1,'U370TIOT entered'
         UDIAG 3,'U370TIOT: ddname',(R9),8
         EJECT ,
*----------------------------------------------------------------------
*  Find TIOT, see if ddname allocated
*----------------------------------------------------------------------
         USING PSA,0
         L     R15,PSATOLD             my TCB
         USING TCB,R15
*
         L     R14,TCBTIO              TCB's TIOT
         DROP  R15
         USING TIOT1,R14
*
         LA    R2,TIOENTRY             @ 1st entry
         DROP  R14
         USING TIOENTRY,R2
*
         SLR   R6,R6
*
NEXTENT  DS    0H
         ICM   R6,1,TIOELNGH           TIOT entry length
         BZ    EOT                     no more entries
*
         CLC   TIOEDDNM,0(R9)          found DDNAME?
         BE    FOUND
*
         LA    R2,0(R6,R2)             @ next entry
         B     NEXTENT
*
EOT      DS    0H
         UDIAG 3,'U370TIOT ddname not allocated'
         XC    SARETVAL,SARETVAL       TIOT entry ptr = 0
         B     EXIT
*
FOUND    DS    0H
         UDIAG 3,'U370TIOT ddname allocated'
         ST    R2,SARETVAL             TIOT entry ptr
*----------------------------------------------------------------------
*  Return to caller
*----------------------------------------------------------------------
EXIT     UDIAG 1,'U370TIOT exited'
         L     R1,SARETVAL             return value = TIOT ptr or 0
         SLR   R15,R15
         UPEND R1=PASS                 return to caller w/ R1 intact
*
         LTORG ,
*----------------------------------------------------------------------
*  Dynamic savearea & workarea
*----------------------------------------------------------------------
SAVEAREA DSECT ,
SAVREGS  DS    18F
SARETVAL DS    F                       return value (R1)
         DS    0D
L$SA     EQU   *-SAVEAREA              length of savearea
*
         PRINT NOGEN
         WATGD ,
*
         PRINT GEN
U370TIOT UFINI DXDUTIOT,L$SA
         END   ,
