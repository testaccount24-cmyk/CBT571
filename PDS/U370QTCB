U370QTCB TITLE 'Query TCB'
*
* Copyright 2002 James M. Morrison
*
*----------------------------------------------------------------------
*  To do:
*  1) Use STATUS macro (?) to quiesce other TCBs while examining
*     Lock(s)?
*----------------------------------------------------------------------
         U#SET FUNC=UTIL
*----------------------------------------------------------------------
* Function:    Diagnostic aid
*              Currently controlled by debug level
*
* Entry: R0    ignored
*        R1    ignored
*
* Exit:  R15 = return code = 00
*
* Diagnostics:
*              1 - ABEND retry entered message
*              2 - TCB summary
*              3 - dump each TCB
*              8 - dump ASCB
*              9 - dump ASXB
*
* Notes:
*----------------------------------------------------------------------
* Reg  DSECT    usage
*----------------------------------------------------------------------
* R2
* R3
* R4   TCB
* R5
* R6
* R7
* R8            internal linkage
* R9            subroutine scratch
* R10           internal linkage
*----------------------------------------------------------------------
TPU      DSECT ,
TPUFLG1  DS    X             status flags
TPU1RRR  EQU   1             ESTAE established (only needs doing once)
         DS    0D
L$TPU    EQU   *-TPU
         TITLE ' '
U370QTCB UPROC ID=UQTC,L=L$SA,CPD=(Y,L$TPU,ESTAE)
*----------------------------------------------------------------------
         STM   R0,R2,SAENTRY           save entry parms
         UDIAG 2,SKIP=EXIT             leave now if no real output
         BAL   R10,CLEAR               clear line to blanks
         BAL   R10,PRINTX              print blank line
         UDIAG 2,'QTCB:'               output follows
         U@CPD TPU,R10,ERR=EXIT
         TM    TPUFLG1,TPU1RRR         ESTAE established?
         BO    QGO
         UESTAE BAIL                   just leave if ABEND
         OI    TPUFLG1,TPU1RRR         ESTAE established
QGO      DS    0H
         DROP  R10
*----------------------------------------------------------------------
*  Print ASCB, ASXB; TCBs: current, first & last
*----------------------------------------------------------------------
         USING PSA,0
         MVC   SAM1TXT2(4),=C'ASCB'
         MVC   SAM1TXT3(4),=C'ASXB'
         MVC   SAM1TXT4(7),=C'PSATOLD'
         MVC   SAM1TXT5(8),=C'ASXBFTCB'
         MVC   SAM1TXT6(8),=C'ASXBLTCB'
         BAL   R10,PRINT               print title line
*
QASCB    L     R2,PSAAOLD              @ current ASCB
         USING ASCB,R2
         LR    R0,R2
         LA    R1,SAM1TXT2
         BAL   R10,FMTHEX
*
QASXB    L     R3,ASCBASXB             @ ASXB
         USING ASXB,R3
         LR    R0,R3
         LA    R1,SAM1TXT3
         BAL   R10,FMTHEX
*
QCURTCB  L     R0,PSATOLD              @ current TCB
         LA    R1,SAM1TXT4
         BAL   R10,FMTHEX
*
QFTCB    L     R0,ASXBFTCB             @ first TCB
         LA    R1,SAM1TXT5
         BAL   R10,FMTHEX
*
QLTCB    L     R0,ASXBLTCB             @ last  TCB
         LA    R1,SAM1TXT6
         BAL   R10,FMTHEX
*
         BAL   R10,PRINT               print data line
         EJECT ,
*----------------------------------------------------------------------
*  SNAP ASCB, ASXB, current TCB
*----------------------------------------------------------------------
SNAP     DS    0H
         UDIAG 8,'QTCB ASCB',(R2),L$ASCB
         UDIAG 9,'QTCB ASXB',(R3),L$ASXB
         L     R4,PSATOLD              @ current TCB
         USING TCB,R4
         BAL   R10,DISPTCB
         EJECT ,
*----------------------------------------------------------------------
*  Chase TCB chain
*----------------------------------------------------------------------
CHASE    BAL   R10,PRINT
         MVI   SAM1FRST,C'-'
         MVC   SAM1FRST+1(SAM1LAST-SAM1FRST-1),SAM1FRST
         MVC   SAM1TXT5(11),=C'TCB SUMMARY'
         BAL   R10,PRINT
         MVC   SAM1TXT2(3),=C'TCB'
         MVC   SAM1TXT3(3),=C'CMP'
         MVC   SAM1TXT4(3),=C'OTC'
         MVC   SAM1TXT5(3),=C'LTC'
         MVC   SAM1TXT6(3),=C'NTC'
         MVC   SAM1TXT7(5),=C'JSTCB'
         MVC   SAM1TXT8(4),=C'BACK'
         MVC   SAM1TXTP(2),=C'PK'
         MVC   SAM1TXTF(4),=C'FLGS'
         BAL   R10,PRINT
*
         L     R4,ASXBFTCB             @ first TCB
CHASLOOP BAL   R8,SUMTCB
         ICM   R4,15,TCBTCB            @ next TCB
         BNZ   CHASLOOP
*
EXIT     DS    0H
         UPEND ,                       return to caller
         EJECT ,
*----------------------------------------------------------------------
*  TCB summary - setup output - R8 return R4 TCB
*----------------------------------------------------------------------
SUMTCB   DS    0H
         LR    R0,R4
         LA    R1,SAM1TXT2
         BAL   R10,FMTHEX
*
         L     R0,TCBCMP
         LA    R1,SAM1TXT3
         BAL   R10,FMTHEX
*
         L     R0,TCBOTC
         LA    R1,SAM1TXT4
         BAL   R10,FMTHEX
*
         L     R0,TCBNTC
         LA    R1,SAM1TXT5
         BAL   R10,FMTHEX
*
         L     R0,TCBLTC
         LA    R1,SAM1TXT6
         BAL   R10,FMTHEX
*
         L     R0,TCBJSTCB
         LA    R1,SAM1TXT7
         BAL   R10,FMTHEX
*
         L     R0,TCBBACK
         LA    R1,SAM1TXT8
         BAL   R10,FMTHEX
*
         IC    R0,TCBPKF
         SLR   R1,R1
         BAL   R10,FMTHEX
         STCM  R1,3,SAM1TXTP
*
*  Divide TCBFLGS: FLG1-FLG3, FLG4-FLG5
*  This places the dispatchability flags together nicely
*
         ICM   R0,15,TCBFLGS           C'112233-4455' (easier to read)
         SLR   R1,R1                    +0123456789A
         BAL   R10,FMTHEX
         STCM  R0,15,SAM1TXTF+00         0000
         STCM  R1,12,SAM1TXTF+04             11
         MVI   SAM1TXTF+06,C'-'
         STCM  R1,03,SAM1TXTF+07                11
         ICM   R0,15,TCBFLGS+4
         SLR   R1,R1
         BAL   R10,FMTHEX
         STCM  R0,12,SAM1TXTF+09                  00
*
         BAL   R10,PRINT
         BR    R8
*----------------------------------------------------------------------
*  Format fullword (display hex) - R0 = value, R1 @ output slot
*----------------------------------------------------------------------
FMT@TCB  DS    0H
         LR    R0,R4                   @ TCB
         LA    R1,SAM1TXT3
FMTHEX   DS    0H
         UCALL U370HEX
         BR    R10
*----------------------------------------------------------------------
*  DISPTCB - Display TCB (R4); R9 scratch
*----------------------------------------------------------------------
DISPTCB  DS    0H
         LA    R9,TCBPXLEN
         SR    R4,R9
         UDIAG 3,'QTCB TCB prefix',(R4),TCBPXLEN
         AR    R4,R9
         UDIAG 3,'QTCB TCB proper',(R4),TCBMNLEN
         L     R9,TCBEXT2
         UDIAG 3,'QTCB TCB extension',(R9),TCBX2LEN
         UDIAG 3,' '
         BR    R10
*----------------------------------------------------------------------
*  Print message
*----------------------------------------------------------------------
PRINT    DS    0H
         MVC   SAM1TXT1,=C'<2>'
PRINTX   UDIAG 2,SAMSG,L$SAMSG
CLEAR    MVI   SAMSG,C' '
         MVC   SAMSG+1(L$SAMSG-1),SAMSG
         BR    R10
*----------------------------------------------------------------------
*  ESTAE "retry" routine
*----------------------------------------------------------------------
BAIL     DS    0H
         UDIAG 1,'QTCB BAIL; results incomplete'
         B     EXIT
         LTORG ,
*----------------------------------------------------------------------
*  Data areas
*----------------------------------------------------------------------
         LTORG ,
*----------------------------------------------------------------------
*  Dynamic savearea & workarea - based R13
*----------------------------------------------------------------------
SAVEAREA DSECT ,
SASA     DS    18F                standard MVS savearea
SAENTRY  DS    3F                 R0:R2 from entry
*
SAM1TXT1 DS    C'<3> '
SAM1TXT2 DS    CL9                TCB addr
         DS    CL2
SAM1FRST DS    0C
SAM1TXT3 DS    CL8                TCBCMP
         DS    CL2
SAM1TXT4 DS    CL8                TCBOTC
         DS    CL2
SAM1TXT5 DS    CL8                TCBNTC
         DS    CL2
SAM1TXT6 DS    CL8                TCBLTC
         DS    CL2
SAM1TXT7 DS    CL8                TCBJSTCB
         DS    CL2
SAM1TXT8 DS    CL8                TCBBACK
         DS    CL2
SAM1TXTP DS    CL2                TCBPKF
         DS    CL2
SAM1TXTF DS    CL11               TCBFLGS: 112233 4455
SAM1LAST DS    0C
L$SAMSG  EQU   *-SAM1TXT1
SAMSG    EQU   SAM1TXT1,L$SAMSG
SAX      DS    0D
L$SA     EQU   *-SAVEAREA         length of savearea
*
         WATGD ,
*
         PRINT NOGEN
         IHAPSA ,
         IHAASCB ,
L$ASCB   EQU   *-ASCB
         IHAASXB ,
L$ASXB   EQU   *-ASXB
         IKJTCB DSECT=YES,LIST=NO
*
U370QTCB UFINI DXDUQTCB,L$SA
         END   ,
