STCP370  TITLE 'MVS38j TCP/IP subsystem - main'
*----------------------------------------------------------------------
*  Copyright 2002, James M. Morrison
*----------------------------------------------------------------------
*  Things to do:
*----------------------------------------------------------------------
         U#SET FUNC=STCP
*----------------------------------------------------------------------
*  Diagnostics:
*              1 - RETRY entered
*              2 - trace msgs
*              3 - RETRY regs
*              4 - subtask - WUD @ entry
*----------------------------------------------------------------------
* Notes:
*----------------------------------------------------------------------
* R2
* R3
* R4
* R5
* R6
* R7
* R8
* R9  WUD      subtask mode
* R10          our real program base reg
* R11          STCPWA ... ALWAYS (contains our savearea)
* R12          BASE REG ... ALL CSECTS
* R13          savearea
*----------------------------------------------------------------------
         TITLE ' '
STCP370  UPROC ID=MAIN,L=L$SA,CPD=(Y,0,ESTAE)
***
*----------------------------------------------------------------------
***
         OI    TGDFLG4,TGD4NSTA        don't want ANY ESTAEs
**
*----------------------------------------------------------------------
**
         L     R1,TGD@PARM             save entry parms
         LTR   R1,R1
         BZ    SUBSYS
         CLC   =C'WUD',0(R1)           subtask?
         BNE   SUBSYS
*----------------------------------------------------------------------
*  Subtask support
*----------------------------------------------------------------------
SUBTASK  DS    0H
         LR    R9,R1                   @ WUD
         USING WUD,R9
         MVC   TGDLOGD,WUDLOGD         set U370LOG DDname
         UDIAG 0,'TCP/IP subtask active'
         UDIAG 4,'370 subtask WUD',(R9),L$WUD
         UCALL U370DBUG                handle xxxxDBUG
         UCALL U370QTCB                ASCB, ASXB, TCB diag info
         ICM   R0,8,=AL1(TSKM$NEW)     new TCB - setup task
         LR    R1,R9
         UCALL STCPTSKM                subtask task manager
         DROP  R9
         UWTO  2,'370 sub  EXIT'
         B     TASKFIN                 clean up & exit
*----------------------------------------------------------------------
*  Subsystem initialization
*----------------------------------------------------------------------
SUBSYS   DS    0H
         ULOG 'STCP370 v00.00 Copyright 2002, James M. Morrison'
         UCALL U370DBUG                handle xxxxDBUG
         UQENV ,                       query APF
         TM    TGDFLG2,TGD2APF         APF authd?
         BO    AUTHOK
         UWTO  0,'TCP/IP APF error'
         MVI   SAREASON,X'47'          not authorized
UABEND   UABEND SAREASON               ABEND w/ diagnostics
AUTHOK   DS    0H
         UESTAE RETRY                  establish ESTAE environment
*
         UDIAG 2,'370 - INIT'
         UCALL STCPSS00,ERR=SHUT       start up subsystem
         OI    STPFLG1,STP1INIT
         UCALL STCPOPR,ERR=SHUT        start up opr interface
*
*  STCPOPR will start up the 3088 interface, since the cuu the user
*  wishes to use may be specified on the TCP/IP start command
*
         NI    STPFLG1,255-STP1INIT
*----------------------------------------------------------------------
*  Run TCP/IP
*----------------------------------------------------------------------
MON      DS    0H
         TM    STPFLG1,STP1ABRT                  abort startup?
         BO    SHUT
         UWTO  2,'370 - MON'
         ICM   R0,8,=AL1(TSKM$MON)               monitor subtasks
         SLR   R1,R1                             no WUD - main task
         UCALL STCPTSKM                          manage subtasks
*----------------------------------------------------------------------
*  Termination
*----------------------------------------------------------------------
SHUT     DS    0H
         UWTO  2,'370 - SHUT'
         OI    STPFLG1,STP1SHUT        shutdown appl
         UCALL STCPSS99                shut subsystem
         UCALL STCPOPR                 shut opr interface
         UWTO  2,'370 main EXIT'
*----------------------------------------------------------------------
*  Return to caller
*----------------------------------------------------------------------
TASKFIN  DS    0H
RESTART  DS    0H
EXIT     DS    0H
         UPEND ,                       terminate application
*----------------------------------------------------------------------
*  ESTAE retry routine
*----------------------------------------------------------------------
RETRY    DS    0H
         STM   R0,R15,SAREGS       diag aid
         UWTO  1,'370 RETRY routine called'
         UDIAG 3,'370 retry regs',SAREGS,16*4
         B     RESTART
*----------------------------------------------------------------------
         LTORG ,
*
         EJECT ,
         WASTCP ,
         DSWUD ,                            Work Unit Descriptor
*
         EJECT ,
SAVEAREA DSECT ,
         DS    18F
SAREGS   DS    16F
SAENTRY  DS    F                            entry R1
SAREASON DS    X
SAEND    DS    0D
L$SA     EQU   *-SAVEAREA
*
         PRINT GEN
STCP370  UFINI DXDTGD,L$STCP,ID=TGD         TGD + STCPWA
STCP370  UFINI DXDS370,L$SA                 CSECT workarea
*
         END   ,
