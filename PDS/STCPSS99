STCPSS99 TITLE 'TCP/IP subsystem terminatino'
*
* Copyright 2002 James M. Morrison
*
*----------------------------------------------------------------------
*  To do:
*----------------------------------------------------------------------
         U#SET FUNC=STCP
*----------------------------------------------------------------------
* Function:    Terminates TCP/IP subsystem
*
*
* Entry:  no parms, supervisor state, key 8
*
* Exit:   R15 =  0 - OK; supervisor state, key 8
*
* Notes:
*----------------------------------------------------------------------
STCPSS99 UPROC ID=SS99,L=L$SA
         UDIAG 1,'SS99 enter'
*----------------------------------------------------------------------
*  Find our SSCVT
*----------------------------------------------------------------------
         L     R15,=V(FINDSSCT)
         BALR  R14,R15                 find TCP/IP SSCVT
         LTR   R9,R1                   found?
         BZ    EXIT                    no, nothing to do
         USING SSCT,R9
*
SSFOUND  DS    0H
         L     R8,SSCTSUSE             @ SGD
         USING SGD,R8
         L     R7,SGD@SQH              @ SQH
         USING SQH,R7
         L     R6,SSCTSSVT             @ SSVT
         USING SSVT,R6
         TM    STPFLG1,STP1SHUT        main task requesting shutdown?
         BO    SHUT                    good enough, br
*----------------------------------------------------------------------
*  Drain the subsystem
*----------------------------------------------------------------------
DRAIN    DS    0H
         TM    SQHFLG1,SQH1DRN         already started draining?
         BO    SHUT                    yes, shut it down
         SPKA  0
         OI    SQHFLG1,SQH1DRN         stop queueing up work
         UWTO  0,'TCP/IP draining'
         B     EXIT
*----------------------------------------------------------------------
*  Shut down the subsystem
*----------------------------------------------------------------------
SHUT     DS   0H
*
*  Call STCPSVC9 to de-install SVC before freeing SGD, since
*  STCPSVC9 may need FREEMAIN info from the SGD if the SVC was
*  installed into CSA
*
         UCALL STCPSVC9                de-install TCP/IP SVC
*
         SPKA  0
         SLR   R0,R0
         ST    R0,SSCTSSVT             SSCVT -> SSVT = 0
         ST    R0,SSCTSUSE             SSCVT -> SGD  = 0
         ST    R0,STP@SGD              STP -> SGD = 0
         ST    R0,STP@SQH              STP -> SQH = 0
         ST    R0,STP@SSVT             STP -> SSVT = 0
*
         L     R0,SGDLSSFN
         LTR   R0,R0
         BZ    FREESGD
         L     R1,SGD@SSFN
         FREEMAIN R,LV=(0),A=(1)       free function code CSA
         UDIAG 2,'SS99 freed ssfn'
*
FREESGD  L     R0,SGDLCSA1
         LTR   R0,R0
         BZ    EXIT
         FREEMAIN R,LV=(0),A=(R8)      free SGD, SQH, SSVT
         UDIAG 2,'SS99 freed SGD, SQH, SSVT'
*
EXIT     DS    0H
         SPKA  8*16
         UWTO  2,'SS99 call QSS'
         SLR   R0,R0
         SLR   R1,R1
         UCALL U370QSS                 debug aid - show SS info
         UWTO  2,'SS99 ret  QSS'
*
         UDIAG 1,'SS99 exit'
         UPEND ,                       return to caller
         EJECT ,
*----------------------------------------------------------------------
*  Data areas
*----------------------------------------------------------------------
         LTORG ,
SAVEAREA DSECT ,
SASA     DS    18F
         DS    0D
L$SA     EQU   *-SAVEAREA
*
         DSSSGD ,                      TCP/IP subsys global data
         DSSSQH ,                      TCP/IP subsys queue headers
         WASTCP ,                      TCP/IP common
*
         PRINT OFF
         CVT    DSECT=YES,LIST=NO
         IEFJESCT ,                    JESCT
         IEFJSCVT ,                    SSCVT
         IEFJSSVT ,                    SSVT
         PRINT ON
*
STCPSS99 UFINI DXDSSS99,L$SA
         END   ,
