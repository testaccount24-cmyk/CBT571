U370DATE TITLE 'Date and time conversion'
*
* Copyright 2002 James M. Morrison
*
*----------------------------------------------------------------------
         U#SET FUNC=UTIL
*----------------------------------------------------------------------
* Function:    Date & time conversion
*
* Entry:  R0 = conversion type:
*              00 - TIME DEC Julian to EBCDIC Gregorian
*         R1 as below:
*
*         Type=00 - TIME DEC Julian to EBCDIC Gregorian
*              +00  HHMMSSth               TIME DEC R0
*              +04  00YYDDDF               TIME DEC R1
*              +08  CL16'YYYYMMDDHHMMSSTH' output buffer
*
* Exit:   R15 =  0 - conversion successful
*                8 - unrecognized type request
*
* Notes:
*----------------------------------------------------------------------
U370DATE UPROC ID=UDAT,L=L$SA
         STM   R0,R1,SAENTRY           save entry parms
         LA    R2,8                    assume unrecognized request
         LTR   R0,R0                   TIME DEC Julian to EBCDIC Greg?
         BNZ   EXIT
         LR    R9,R1                   parm ptr
*----------------------------------------------------------------------
*  Convert HHMMSSth, YYYY to EBCDIC
*----------------------------------------------------------------------
         LM    R4,R5,0(R9)             HHMMSSth,00YYDDDF
         STM   R4,R5,SAARG
         AP    SAARG+4(4),=P'1900000'  Y2K: add S/370 epoch century
         UNPK  SACHR,SAARG             packed to EBCDIC
         OI    SACHRX,X'F0'            repair sign
*----------------------------------------------------------------------
*  Convert year to binary
*----------------------------------------------------------------------
         L     R3,SAARG+4              YYYYDDDF
         SRL   R3,16-4                 000YYYY.
         ST    R3,SAPAKY
         OI    SAPAKY+3,X'0F'          packed year
         CVB   R3,SADWD
         ST    R3,SABINY               binary year
*----------------------------------------------------------------------
*  Select month table
*----------------------------------------------------------------------
         LA    R8,NOTLEAP              not a leap year
         TM    SABINY+3,X'03'          divisible by 4?
         BC    5,CALCMON               no, can't be leap year
         SLR   R6,R6
         LA    R10,400                 divisible by 400 is leap year
         LR    R7,R3
         DR    R6,R10
         LTR   R6,R6
         BNZ   CALCMON                 evenly divisible
         LA    R10,100                 divisible by 100 not leap year
         LR    R7,R3
         DR    R6,R10
         LTR   R6,R6
         BZ    CALCMON                 evenly divisible
SETLEAP  LA    R8,LEAP                 leap year
*----------------------------------------------------------------------
*  Find month & month day, given Julian days DDD in year
*----------------------------------------------------------------------
CALCMON  DS    0H                      R8 @ month table
         LH    R0,SAPAKDDD             DDDF
         STH   R0,SAPAKD
         CVB   R5,SADWD2
         ST    R5,SABIND               binary ddd
*
         LA    R1,1
         SLR   R14,R14                 month minus one
         SLR   R15,R15
SCANMON  IC    R15,0(R14,R8)           # days in month
         CR    R5,R15                  too many?
         BNH   SETMON                  no, br; now know month
         SR    R5,R15                  reduce ddd
         AR    R14,R1                  bump month
         B     SCANMON
SETMON   DS    0H
         LA    R1,100                  decimal shift factor
         SLR   R6,R6
         LA    R7,1(,R14)              month
         MR    R6,R1
         AR    R7,R5                   binary month, day of month
         CVD   R7,SADWD3               decimal: 0000 0000 000M MDDF
         OI    SAPAKMDX,X'0F'          assure reasonable sign
         UNPK  SACHRMD,SAPAKMD         MMDD to EBCDIC
*----------------------------------------------------------------------
*  Return data to caller
*----------------------------------------------------------------------
         MVC   08(8,R9),SARESULT+8     YYYYMMDD
         MVC   16(8,R9),SARESULT+0     HHMMSSth
         SLR   R2,R2                   success
EXIT     DS    0H
         UDIAG 2,'DATE sa',(RSA),L$SA
         UPSR  1,'DATE rc',(R2)
         UPEND RC=(R2)                 return to caller
         B     EXIT
         LTORG ,
*                  J  F  M  A  M  J  J  A  S  O  N  D
NOTLEAP  DC    AL1(31,28,31,30,31,30,31,31,30,31,30,31)
LEAP     DC    AL1(31,29,31,30,31,30,31,31,30,31,30,31)
*----------------------------------------------------------------------
*  Dynamic savearea & workarea
*----------------------------------------------------------------------
SAVEAREA DSECT ,
SASA     DS    18F
SAENTRY  DS    2F                      R0:R1 from entry
*
SADWD    DS    D                       year
SABINY   EQU   SADWD+0,4               binary
SAPAKY   EQU   SADWD+4,4               packed 000Y,YYYF
*
SADWD2   DS    D                       julian day of year
SABIND   EQU   SADWD2+0,4              binary
SAPAKD   EQU   SADWD2+6,2              packed DDDF
*
SADWD3   DS    D                       gregorian month, day of month
SABINMD  EQU   SADWD3+0,4              binary 0000MMDD
SAPAKMD  EQU   SADWD3+5,3              packed   0MMDDF
SAPAKMDX EQU   *-1,1                   sign repair
*
SAARG    DS    D                       HHMMSSth,YYYYDDDF
SAPAKDDD EQU   SAARG+6,2              +0 1 2 3  4 5 6 7
*
SARESULT DS    0CL16                   nearly final result
SACHR    DS    0CL15
SACHRTM  DS    C'HHMMSSth'
SACHRY   DS    C'19YY'
SACHRD   DS    C'DDD'
SACHRX   EQU   *-1,1                   sign repair
         DS    C' '
SACHRMD  EQU   SACHRD,4                C'MMDD'
         DS    0D                      align
L$SA     EQU   *-SAVEAREA              length of savearea
*
         PRINT NOGEN
         WATGD ,
U370DATE UFINI DXDUDAT,L$SA
         END   ,
